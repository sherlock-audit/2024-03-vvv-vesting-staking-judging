Special Hotpink Narwhal

high

# Withdrawn/finished stakes will still accrue rewards if the durationToMultiplier is increased

## Summary

The way that the staking mechanism is set in the vVv protocol allows old stakes which have finished, have been withdrawn to still accrue rewards in case the `durationToMultiplier` is ever increased. 

This will allow a user which has staked only once (and later unstaked/withdrawn) to accrue rewards for the rest of the protocol lifetime as their stake will never get erased if the `durationToMultiplier` is ever increased or it keeps increasing. This will lead to significant vVv token inflation over time.

## Vulnerability Detail
When users create a new stake, the following StakeData is set:

```solidity

 userStakes[msg.sender][stakeId] = StakeData({
            stakedEthAmount: uint224(_stakedEthAmount),
            stakeStartTimestamp: uint32(block.timestamp),
            stakeIsWithdrawn: false,
            stakeDuration: _stakeDuration
        });

```
When users claim their rewards in the form of vVv tokens, they're tracked in this mapping:
`userVvvClaimed[msg.sender] += _vvvAmount;`

After claiming (or before doing so), when the staking period has ended, users can withdraw their stake, when they do so, the withdrawals are tracked by changing the `stakeIsWithdrawn` to `false`, preventing users for withdrawing their stake more than once. But their `stakeId`, `stakeStartTimestamp`, `stakedEthAmount`, as well as the `stakeDuration` still remain in the system and are not reset/defaulted, etc.

The mechanism which prevents users claiming more tokens than they're eligible to, meaning they can't claim more than once the amount that they're entitled to according to their stake amount, stake duration and the `durationToMultiplier` due to the following condition in the `calculateClaimableVvvAmount()` function: 

`return calculateAccruedVvvAmount() - userVvvClaimed[msg.sender];`

But that condition doesn't safeguard against accruing rewards when the multiplier increases. If the `durationToMultiplier` is increased, all of the stake data (mentioned above) still remains active and since the `calculateAccruedVvvAmount()` will be greater than the `userVvvClaimed`, this allow for users to claim more rewards for stakes that they've unstaked and already claimed rewards for. 

This happens due to the way that claimable vVv tokens are calculated, since all of the stake data remains the same and only the maximum `stakeDuration` is taken into account, the `accruedVvv` can increase each time when the multipliers are changed: 

```solidity

     unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration ? stakeDuration : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

            accruedVvv =
                (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) / DENOMINATOR;
        }

```

**PoC**

The following PoC can be inserted into the "VVVETHStaking.unit.t.sol" testing suite:

```solidity
    function testWithdrawNew() public {
        vm.startPrank(sampleUser, sampleUser);
        uint256 stakeEthAmount = 1 ether;
        uint256 stakeId = EthStakingInstance.stakeEth{value: stakeEthAmount}(VVVETHStaking.StakingDuration.ThreeMonths);

        // forward to first timestamp with released stake
        advanceBlockNumberAndTimestampInSeconds(
            EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths) + 1
        );

        uint256 balanceBefore = address(sampleUser).balance;
        console.log("Balance Before WWL + Claim: ", balanceBefore);

        //Claim vVv

        uint256 claimableVvv = EthStakingInstance.calculateClaimableVvvAmount();

        EthStakingInstance.claimVvv(claimableVvv);
        uint256 vvvBalanceAfterClaim = VvvTokenInstance.balanceOf(sampleUser);

        console.log("Claimable vVv: ", claimableVvv);
        console.log("Balance Before WWL, but after Claim: ", vvvBalanceAfterClaim);

        //Withdraw stake

        EthStakingInstance.withdrawStake(stakeId);
        uint256 balanceAfter = address(sampleUser).balance;
        console.log("Balance After WWL + Claim: ", balanceAfter);

        //Ensure no claimable vVv

        uint256 claimableVvv1 = EthStakingInstance.calculateClaimableVvvAmount();

        console.log("Claimable stake after claim: ", claimableVvv1);

        vm.stopPrank();

        //Change multiplier values

        vm.startPrank(ethStakingManager, ethStakingManager);
        VVVETHStaking.StakingDuration[] memory durations = new VVVETHStaking.StakingDuration[](3);
        durations[0] = VVVETHStaking.StakingDuration.ThreeMonths;
        durations[1] = VVVETHStaking.StakingDuration.SixMonths;
        durations[2] = VVVETHStaking.StakingDuration.OneYear;

        uint256[] memory multipliers = new uint256[](3);
        multipliers[0] = 25_000;
        multipliers[1] = 30_000;
        multipliers[2] = 35_000;

        EthStakingInstance.setDurationMultipliers(durations, multipliers);

        vm.stopPrank();

        //Check claimable vVv amounts have increased again

        vm.startPrank(sampleUser, sampleUser);

        uint256 claimableVvv2 = EthStakingInstance.calculateClaimableVvvAmount();

        console.log("Balance of new claimable after multiplier increase: ", claimableVvv2);

        vm.stopPrank();
    }
```

## Impact
All the users who have ever staked from the beginning of the protocol will be eligible to claim accrued rewards forever, even if they've unstaked/withdrawn/claimed rewards if the `durationToMultiplier` is ever increased. This can lead to significant vVv token inflation. 

## Code Snippet
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L166

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L189

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L206-L210

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L230-L238

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L264-L270

## Tool used

Manual Review

## Recommendation

There are few ways into which this can be mitigated. A significant change in protocol architecture to exclude stakes which have been unstaked and/or saving the multiplier used during the time when the stake was made as a struct element and if any changes do happen on the global level for them to affect only new stakes. 
