Sharp Mint Llama

medium

# Claiming VVV Tokens can get very gas expensive/reach above block gas limit

## Summary

The `VVVETHStaking::claimVvv` function eventually uses the `VVVEthStaking::calculateAccruedVvvAmount` function which implements an unbounded for loop.

This for loop essentially runs as many times as the deposits the `msg.sender` address has made in his life-time. This means that after a big amount of deposits (which is possible for an address that is maybe DCA-ing in the protocol for years etc.), claiming the rewards will become very gas expensive. It is also possible for an address to be Denied Of Service because of this.

## Vulnerability Detail

```javascript
    /**
        @notice Claims $VVV for a user
        @param _vvvAmount The amount of $VVV to claim
     */
    function claimVvv(uint256 _vvvAmount) external {
        if (_vvvAmount == 0) revert CantClaimZeroVvv();

@>      uint256 claimableVvv = calculateClaimableVvvAmount();
        if (_vvvAmount > claimableVvv) revert InsufficientClaimableVvv();

        userVvvClaimed[msg.sender] += _vvvAmount;

        vvvToken.safeTransfer(msg.sender, _vvvAmount);

        emit VvvClaim(msg.sender, _vvvAmount);
    }

    function calculateClaimableVvvAmount() public view returns (uint256) {
@>      return calculateAccruedVvvAmount() - userVvvClaimed[msg.sender];
    }

    function calculateAccruedVvvAmount() public view returns (uint256) {
        uint256[] memory stakeIds = _userStakeIds[msg.sender];
        if (stakeIds.length == 0) return 0;

        uint256 totalVvvAccrued;

@>      for (uint256 i = 0; i < stakeIds.length; ++i) {
            StakeData memory stake = userStakes[msg.sender][stakeIds[i]];
            unchecked {
                totalVvvAccrued += calculateAccruedVvvAmount(stake);
            }
        }

        return totalVvvAccrued;
    }
```

## Impact

An address with a big amount of deposits after years of depositing in the protocol, can be potentially denied his rewards because of the unbounded for loop.

## Code Snippet

[https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L196-L214](url)

## Proof of Code

Place the following code in the `test/staking/VVVETHStaking.unit.t.sol` file:

```javascript
    uint256 private checkpointGasLeft = 1; // Start the slot warm.

    function testClaimVvvGas() public {
        vm.startPrank(sampleUser, sampleUser);
        uint256 stakeEthAmount = 0.000001 ether;

        for(uint256 i = 0; i < 10000; i++){
            EthStakingInstance.stakeEth{ value: stakeEthAmount }(VVVETHStaking.StakingDuration.ThreeMonths);
        }
  
        advanceBlockNumberAndTimestampInSeconds(EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths) + 20);

        uint256 claimableVvv = EthStakingInstance.calculateClaimableVvvAmount();

        checkpointGasLeft = gasleft();
        EthStakingInstance.claimVvv(claimableVvv);
        uint256 checkpointGasLeft2 = gasleft();

        // Subtract 100 to account for the warm SLOAD in startMeasuringGas.
        uint256 gasDelta = checkpointGasLeft - checkpointGasLeft2 - 100;
        console.log("gas used: ", gasDelta);

        vm.stopPrank();
    }
```
```javascript
Logs:
  gas used:  22067428
```
## Tool used

Manual Review, Foundry

## Recommendation

Consider rethinking the way you calculate the rewards. It shouldn't be necessary to calculate the rewards of all the deposits of an address' lifetime. 
