Crazy Champagne Duck

medium

# Potential DOS when claiming rewards due to ease of unchecked increase in users' stakes

## Summary

The protocol makes some design choice which will lead to an inevietable growth of the number of stake positions users can have, and will eventually DOS their ability to claim their vvv tokens.

## Vulnerability Detail

Users have the ability to stake their ETH by calling the `stakeEth` function which goes on to increase the stakeId while pushing the stake into the user's positions. 

```solidity
    function stakeEth(
        StakingDuration _stakeDuration
    ) external payable whenStakingIsPermitted returns (uint256) {
        _stakeEth(_stakeDuration, msg.value);
        return stakeId;
    }
```

```solidity
    ///@notice Private function to stake ETH, used by both stakeEth and restakeEth
    function _stakeEth(StakingDuration _stakeDuration, uint256 _stakedEthAmount) private {
...

        _userStakeIds[msg.sender].push(stakeId); //@note pushed

...
    }
```
Ideally, this shouldn't be a problem, however, certain parts of the protocol's design make it easy for userStakes to easily grow into a large amount.

1. First, the protocol offers no way of popping these position when users withdraw their stakes. 

```solidity
    function withdrawStake(uint256 _stakeId) external {
        StakeData storage stake = userStakes[msg.sender][_stakeId];
        _withdrawChecks(stake);

        stake.stakeIsWithdrawn = true;
        (bool withdrawSuccess, ) = payable(msg.sender).call{ value: stake.stakedEthAmount }("");
        if (!withdrawSuccess) revert WithdrawFailed();
...
    }
```

2. Next, the default reward multiplier implemented upon deployment incentivizes creating position for shorter time periods, hence more positions are created. 

```solidity
        durationToMultiplier[StakingDuration.ThreeMonths] = 10_000;
        durationToMultiplier[StakingDuration.SixMonths] = 15_000;
        durationToMultiplier[StakingDuration.OneYear] = 30_000;
```
This is used when calculating how much vvv a user's position has accrued. 
```solidity
                accruedVvv =
                (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
                DENOMINATOR;
```
Now, looking at the duration multipliers and how the accrued vvv is calculated, it can be inferred that in a way, its more profitable to stake for the short term and restake, than it is for a long term.

> If `nominalAccruedEth` == 1 , `ethToVvvExchangeRate` == 1;, then accruedVvv for staking for 1 year is as follows;

> If StakingDuration.ThreeMonths, accruedVvv == 1 * 1 * 10_000/10_000 == 1 vvv; hence for 1 year, accruedVvv = 360/90 * 1 = 4 vvvs;

> If StakingDuration.SixMonths, accruedVvv == 1 * 1 * 15_000/10_000 == 1.5 vvv; hence for 1 year, accruedVvv = 360/180 * 1.5 = 3 vvvs;

> If StakingDuration.OneYear, accruedVvv == 1 * 1 * 30_000/10_000 == 3 vvv; hence for 1 year, accruedVvv = 360/360 * 1.5 = 3 vvvs;

What this shows is that,  rather than hold one stake position for 1 year, users will be incentivized to hold four stake positions for 3 months each. As established earlier, users have stake positions created and pushed when they stake and not popped when they withdraw their stake. The same also hold when they use the `restakeEth` function to restake without withdrawal. It simply pushes a new stake position into user's stakes. 

```solidity
    function restakeEth(
        uint256 _stakeId,
        StakingDuration _stakeDuration
    ) external whenStakingIsPermitted returns (uint256) {
        StakeData storage stake = userStakes[msg.sender][_stakeId];
        _withdrawChecks(stake);
        stake.stakeIsWithdrawn = true;
        _stakeEth(_stakeDuration, stake.stakedEthAmount);
        return stakeId;
    }
```
This, though will be partially fixed when the admins call the `setDurationMultipliers` function to readjust the multipliers and restraighten the incentives. However, it still won't stop users from creating shorter positions, the incentives are just lesser.

3. Finally, the absence of a function for users to add to their stake. Most staking protocols hold an `addStake` function or its equivalent. The absence of this means that users will have to create a new position alltogether increasing the number of stakes that is pushed into their list of positions. 

All these seemingly low issues combined together creates a fertile ground for an issue like this to occur.

As can be seen when claiming rewards, the entirety of user's positions are looped through. An at a certain point the transaction will always run out of gas due to the excess number of stake positions a user might have accumulated over a period of time.

User calls the `claimVvv` function.
```solidity
    function claimVvv(uint256 _vvvAmount) external {
        if (_vvvAmount == 0) revert CantClaimZeroVvv();

        uint256 claimableVvv = calculateClaimableVvvAmount();//@note the call
        if (_vvvAmount > claimableVvv) revert InsufficientClaimableVvv();

        userVvvClaimed[msg.sender] += _vvvAmount;

        vvvToken.safeTransfer(msg.sender, _vvvAmount);

        emit VvvClaim(msg.sender, _vvvAmount);
    }
```
which calls the `calculateClaimableVvvAmount` 

```solidity
    function calculateClaimableVvvAmount() public view returns (uint256) {
        return calculateAccruedVvvAmount() - userVvvClaimed[msg.sender];
    }
```
The `calculateAccruedVvvAmount` gets all of the user's stakeIds, and loops through them to calculate the `totalVvvAccrued` by a user.
```solidity
    function calculateAccruedVvvAmount() public view returns (uint256) {
        uint256[] memory stakeIds = _userStakeIds[msg.sender];
        if (stakeIds.length == 0) return 0;

        uint256 totalVvvAccrued;
        for (uint256 i = 0; i < stakeIds.length; ++i) { //@note
            StakeData memory stake = userStakes[msg.sender][stakeIds[i]];
            unchecked {
                totalVvvAccrued += calculateAccruedVvvAmount(stake);
            }
        }

        return totalVvvAccrued;
    }
```

```solidity
    function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration
                ? stakeDuration
                : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

            accruedVvv =
                (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
                DENOMINATOR;
        }

        return accruedVvv;
    }
```

## Impact
The impact is seen when users (especially long term protocol users even honest ones) try to claim their `vvv` tokens, due to the adding up of these positions, and the fact that the `claimVvv` function eventually loops through every single stake position, will no longer be able to claim rewards due to out of gas error while calculating their claimablevvv. Whenever this happens, there will be no way to fix things for the users due to the codebase design, hence leading to a permanent loss of funds. 
 
## Code Snippet

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L135
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L301

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L162-L177
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L147-L156

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L249-L251
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L186
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L206
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L222
## Tool used

Manual Review

## Recommendation

A potential workaround to deal with this is to enable automatic claims and removal of staking position from the user's list of positions upon stake withdrawal or restake.
Something like this:

```solidity
    function withdrawStake(uint256 _stakeId) external {
        StakeData storage stake = userStakes[msg.sender][_stakeId];
        _withdrawChecks(stake);

        stake.stakeIsWithdrawn = true;
//calculates the amount that stake has earned, increases the user's vvvClaimed, transfers vvv amount to use and removes the stake from user's list of stakes.
	calculateAccruedVvvAmount(stake);
	userVvvClaimed[msg.sender] += _vvvAmount;
        vvvToken.safeTransfer(msg.sender, _vvvAmount);
        _userStakeIds[msg.sender].pop(_stakeId);

        (bool withdrawSuccess, ) = payable(msg.sender).call{ value: stake.stakedEthAmount }("");
        if (!withdrawSuccess) revert WithdrawFailed();

        emit Withdraw(
            msg.sender,
            _stakeId,
            stake.stakedEthAmount,
            stake.stakeStartTimestamp,
            stake.stakeDuration
        );
    }
```

The same workaround can be done on the `restakeEth` function.


