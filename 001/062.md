Magic Alabaster Anteater

high

# `setDurationMultipliers` will affect past rewards earned by users.

## Summary

User's previously earned rewards can be changed whenever `setDurationMultipliers` function is called.

## Vulnerability Detail

`setDurationMultipliers` is used to set the multiplier for staking periods. This function can only be called by authorized addresses however, it's logic can harm users whenever it is called.

```javascript
    function setDurationMultipliers(
        StakingDuration[] memory _duration,
        uint256[] memory _multipliers
    ) external onlyAuthorized {
        for (uint256 i = 0; i < _duration.length; ++i) {
            durationToMultiplier[_duration[i]] = _multipliers[i];
        }
    }
```

When `durationToMultiplier` value is changed it affects all previously earned and future rewards.

```javascript
function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration
                ? stakeDuration
                : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

            accruedVvv =
                (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
                DENOMINATOR;
        }

        return accruedVvv;
    }
```

We can see that `durationToMultiplier[_stake.stakeDuration]` value is used to calculate the `accruedVVV`. Rewards are calculated as the new multiplier was used during the whole staking period, which might not be true.

Expected behaviour:
1. User staked his ether for 3 months
2. 45 days later protocol decides to change multiplier to `5000`
3. User earned rewards for 45 days with `10000` multiplier and will earn rest of his rewards with `5000` multiplier

Current behaviour:
1. User staked his ether for 3 months
2. 45 days later protocol decides to change multiplier to `5000`
3. user earned rewards for 45 days with `5000` multiplier and will earn rest of his rewards with `5000` multiplier

New `durationToMultiplier` multiplier will be applied to the entire staking period.

Protocol does not have to be malicious to harm users. Current design of `setDurationMultipliers` will always affect previously earned rewards.

PoC

```javascript
function testLossOfRewards() public {
        vm.startPrank(sampleUser, sampleUser);
        uint256 stakeEthAmount = 1 ether;

        EthStakingInstance.stakeEth{ value: stakeEthAmount }(VVVETHStaking.StakingDuration.ThreeMonths);

        vm.stopPrank();

        advanceBlockNumberAndTimestampInSeconds(
            EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths) + 1
        );

        vm.startPrank(ethStakingManager, ethStakingManager);
        VVVETHStaking.StakingDuration[] memory durations = new VVVETHStaking.StakingDuration[](3);
        durations[0] = VVVETHStaking.StakingDuration.ThreeMonths;
        durations[1] = VVVETHStaking.StakingDuration.SixMonths;
        durations[2] = VVVETHStaking.StakingDuration.OneYear;

        uint256[] memory multipliers = new uint256[](3);
        multipliers[0] = 5_000;
        multipliers[1] = 15_000;
        multipliers[2] = 30_000;

        EthStakingInstance.setDurationMultipliers(durations, multipliers);

        vm.stopPrank();

        vm.startPrank(sampleUser, sampleUser);

        uint256 claimableVvv = EthStakingInstance.calculateClaimableVvvAmount();

        assertTrue(claimableVvv == 0.5 ether);

        vm.stopPrank();
    }
```

In provided PoC protocol changed multiplier to `5000` after staking period has ended. User can claim `0.5` ether insted of `1`.

## Impact

User can lose part of his rightfuly earned rewards due to flaw in function logic.

## Code Snippet

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L264-L271

## Tool used

Manual Review

## Recommendation

Create a mechanism that updates rewards for all of users before changing the multiplier.