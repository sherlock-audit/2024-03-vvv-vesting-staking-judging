Tricky Fuchsia Viper

high

# Changing staking `durationToMultiplier` after staking has started will lead to unexpected results


## Summary
Changing staking `durationToMultiplier` after staking has started will lead to unexpected results
## Vulnerability Detail
Within the contract, there's a setDurationMultipliers function which allows change of the staking multiplier.
```solidity
 function setDurationMultipliers(
        StakingDuration[] memory _duration,
        uint256[] memory _multipliers
    ) external onlyAuthorized {
        for (uint256 i = 0; i < _duration.length; ++i) {
            durationToMultiplier[_duration[i]] = _multipliers[i];
        }
    }
```
**POC**
Note: the change of staking multiplier can lead to both overdistributing funds and loss of funds.

Imagine the following overdistributing funds scenario:
1. Bob staking 1 ETH, at the beginning of `durationToMultiplier` for `StakingDuration.ThreeMonths` is 10_000.
2. Three months later, the operator wants to provide more rewards for new  staking period, set `durationToMultiplier` for `StakingDuration.ThreeMonths` to 20_000.
3. Bob  claim his rewards will get 2 $vvv token. (Howerve Bob's theoretical reward value is 1 $vvv token, he claim his rewards before the operator setting `durationToMultiplier` ).

proof of Code:
add this function in `VVVETHStaking.unit.t.sol`
```solidity
function testClaimVvvAfterSetDurationMultipliers() public {

        vm.startPrank(sampleUser, sampleUser);
        uint256 stakeEthAmount = 1 ether;
        EthStakingInstance.stakeEth{ value: stakeEthAmount }(VVVETHStaking.StakingDuration.ThreeMonths);

        // forward to first timestamp with released stake
        advanceBlockNumberAndTimestampInSeconds(
            EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths) + 1
        );

    @>    uint256 claimableVvvBeforeSeting = EthStakingInstance.calculateClaimableVvvAmount();
        console2.log("claimableVvvBeforeSeting: ", claimableVvvBeforeSeting);
        vm.stopPrank();

        //seting multiplier
        vm.startPrank(ethStakingManager, ethStakingManager);
        VVVETHStaking.StakingDuration[] memory durations = new VVVETHStaking.StakingDuration[](3);
        durations[0] = VVVETHStaking.StakingDuration.ThreeMonths;
        durations[1] = VVVETHStaking.StakingDuration.SixMonths;
        durations[2] = VVVETHStaking.StakingDuration.OneYear;

        uint256[] memory multipliers = new uint256[](3);
        multipliers[0] = 20_000;
        multipliers[1] = 27_000;
        multipliers[2] = 33_000;

        EthStakingInstance.setDurationMultipliers(durations, multipliers);
         vm.stopPrank();
        
         vm.startPrank(sampleUser, sampleUser);
    @>  uint256 claimableVvvAfterSetting = EthStakingInstance.calculateClaimableVvvAmount();
        console2.log("claimableVvvAfterSetting: ", claimableVvvAfterSetting);

        uint256 vvvBalanceBefore = VvvTokenInstance.balanceOf(sampleUser);

        //claim the token now
    @>  EthStakingInstance.claimVvv(claimableVvvAfterSetting);
    
    
        uint256 vvvBalanceAfter = VvvTokenInstance.balanceOf(sampleUser);

        assertTrue(vvvBalanceAfter == vvvBalanceBefore + claimableVvvAfterSetting);
       vm.stopPrank();
    }
```
then run : `forge test --mt testClaimVvvAfterSetDurationMultipliers -vv`,
then will get.
```javascript
forge test --mt testClaimVvvAfterSetDurationMultipliers -vv
[⠊] Compiling...
[⠒] Compiling 1 files with 0.8.23
[⠑] Solc 0.8.23 finished in 1.60s
Compiler run successful!

Ran 1 test for test/staking/VVVETHStaking.unit.t.sol:VVVETHStakingUnitTests
[PASS] testClaimVvvAfterSetDurationMultipliers() (gas: 271052)
Logs:
  claimableVvvBeforeSeting:  1000000000000000000
  claimableVvvAfterSetting:  2000000000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 6.22ms (841.00µs CPU time)
```
We can see that sampleUser get more than expected.
## Impact

Loss of funds

## Code Snippet
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L264C5-L271C6

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L222C5-L242C6
## Tool used

Manual Review

## Recommendation
store the multiplier value in storage for each stakeId when it be created.
Then change globle staking multiplier will not impact the historical staking. 