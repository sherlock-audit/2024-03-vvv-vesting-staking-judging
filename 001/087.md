Daring Pink Crab

high

# Calling the setDurationMultipliers causes 2 issues in the protocol

## Summary
Admin can call `setDurationMultipliers` and set higher or lower values for multipliers, however setting higher and lower values causes 2 vulnerablities and due to that the root-cause of both is the same and also the fix for both is the same (see the Recommendation), so i have decided to combine them in 1 report.
## Vulnerability Detail
#### Scenario_1: Where the admins sets a higher value for multiplier
- Assuming the Alice stakes 10 Ethers for 3 months and multiplier for ThreeMonth is 10_000.
- After 3 months, 10 VVV tokens accrues to the Alice (10 means 10e18).
- Alice withdraws their 10 Ethers, also calls `claimVvv` and claims that 10 accrued VVV tokens.
- After some days/months/years, the admin sets higher value for multiplier, for example he changes 10_000 to 20_000.
- Alice again calls `claimVvv` and claims another 10 VVV tokens.

In this case Alice benefits from the previous/finished stakes, until the admin sets higher values for multipliers.
When the Alice calls `claimVvv` for the second time:
```solidity
function claimVvv(uint256 _vvvAmount) external {
        if (_vvvAmount == 0) revert CantClaimZeroVvv();

@>        uint256 claimableVvv = calculateClaimableVvvAmount();
```
the multiplier is set to 20_000 and calculateAccruedVvvAmount() returns 20 (because the new multiplier also affected the previous/finished stakes and the accruedVvv is calculating with the current multiplier, and also the `calculateAccruedVvvAmount` is iterating over the all stakes even the stake is finished), but `userVvvClaimed` for Alice is not updated and returns 10 (10e18 VVV), so `20 -10 = 10` and Alice can claim another 10 VVV tokens.
```solidity
function calculateClaimableVvvAmount() public view returns (uint256) {
        return calculateAccruedVvvAmount() - userVvvClaimed[msg.sender];
    }
```
`calculateAccruedVvvAmount` is iterating over all the stakes, even if the stake is finished and claimed:
```solidity
function calculateAccruedVvvAmount() public view returns (uint256) {
@>        uint256[] memory stakeIds = _userStakeIds[msg.sender];
        if (stakeIds.length == 0) return 0;

        uint256 totalVvvAccrued;
@>        for (uint256 i = 0; i < stakeIds.length; ++i) {
            StakeData memory stake = userStakes[msg.sender][stakeIds[i]];
            unchecked {
                totalVvvAccrued += calculateAccruedVvvAmount(stake);
            }
        }

        return totalVvvAccrued;
    }
```
and due to that the multiplier is increased (from 10_000 increased to 20_000), then `accruedVvv` will be a greater amount and Alice benefits from the previous stakes:
```solidity
function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration
                ? stakeDuration
                : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

            accruedVvv =
@>                (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
                DENOMINATOR;
        }

        return accruedVvv;
    }
```

#### Scenario_2: Where the admins sets a lower value for multiplier
- Assuming the Alice stakes 10 Ethers for 1 year and multiplier for OneYear is 30_000.
- After 1 year, 30 VVV tokens accrues to the Alice (30 means 30e18).
- Alice withdraws their 10 Ethers, also calls `claimVvv` and claims that 30 accrued VVV tokens.
- After some days/months/years, the admin sets lower value for multiplier, for example he changes 30_000 to 10_000.
- Alice stakes 10 Ethers for another year.
- After 1 year, Alice calls `claimVvv` but the transaction reverts and Alice can't claim any VVV tokens.

In this case the `claimVvv` will be DoS for Alice.
When the Alice calls `claimVvv` after the second year:
```solidity
function claimVvv(uint256 _vvvAmount) external {
        if (_vvvAmount == 0) revert CantClaimZeroVvv();

@>        uint256 claimableVvv = calculateClaimableVvvAmount();
```
`userVvvClaimed` returns 30 (30e18) tokens (because Alice claimed 30 tokens after the first year) and  `calculateAccruedVvvAmount()` is returning 20 tokens  and `20 - 30 = -10` will be an underflow. (because the multiplier also affected the first stake of Alice -> for the first stake the `calculateAccruedVvvAmount` should return 30 and for the second stake it should return 10 and sum of them should be 40 and `40 - 30 = 10` and it's OK but it's not doing like this)
```solidity
function calculateClaimableVvvAmount() public view returns (uint256) {
        return calculateAccruedVvvAmount() - userVvvClaimed[msg.sender];
    }
```

### Proof Of Concept
I have demonstrated both scenarios in the PoC, create a new test file under the path `test/staking/` and paste the following code, then run the PoC using the command `forge test --match-contract PoC_1`:
```solidity
//SPDX-License-Identifier: MIT
pragma solidity 0.8.23;

import { VVVAuthorizationRegistry } from "contracts/auth/VVVAuthorizationRegistry.sol";
import { VVVToken } from "contracts/tokens/VvvToken.sol";
import { VVVETHStakingTestBase } from "test/staking/VVVETHStakingTestBase.sol";
import { VVVETHStaking } from "contracts/staking/VVVETHStaking.sol";

/**
 * @title VVVETHStaking Fuzz Tests
 * @dev use "forge test --match-contract VVVETHStakingUnitFuzzTests" to run tests
 * @dev use "forge coverage --match-contract VVVETHStaking" to run coverage
 */
contract PoC_1 is VVVETHStakingTestBase {

     function setUp() public {
        vm.startPrank(deployer, deployer);

        AuthRegistry = new VVVAuthorizationRegistry(defaultAdminTransferDelay, deployer);
        EthStakingInstance = new VVVETHStaking(address(AuthRegistry));
        VvvTokenInstance = new VVVToken(type(uint256).max, 0, address(AuthRegistry));

        //set auth registry permissions for ethStakingManager (ETH_STAKING_MANAGER_ROLE)
        AuthRegistry.grantRole(ethStakingManagerRole, ethStakingManager);
        bytes4 setDurationMultipliersSelector = EthStakingInstance.setDurationMultipliers.selector;
        bytes4 setNewStakesPermittedSelector = EthStakingInstance.setNewStakesPermitted.selector;
        bytes4 setVvvTokenSelector = EthStakingInstance.setVvvToken.selector;
        bytes4 withdrawEthSelector = EthStakingInstance.withdrawEth.selector;
        AuthRegistry.setPermission(
            address(EthStakingInstance),
            setDurationMultipliersSelector,
            ethStakingManagerRole
        );
        AuthRegistry.setPermission(
            address(EthStakingInstance),
            setNewStakesPermittedSelector,
            ethStakingManagerRole
        );
        AuthRegistry.setPermission(
            address(EthStakingInstance),
            setVvvTokenSelector,
            ethStakingManagerRole
        );
        AuthRegistry.setPermission(
            address(EthStakingInstance),
            withdrawEthSelector,
            ethStakingManagerRole
        );

        //mint 1,000,000 $VVV tokens to the staking contract
        VvvTokenInstance.mint(address(EthStakingInstance), 1_000_000 * 1e18);

        vm.deal(sampleUser, 10 ether);
        vm.stopPrank();

        //now that ethStakingManager has been granted the ETH_STAKING_MANAGER_ROLE, it can call setVvvToken and setNewStakesPermitted
        vm.startPrank(ethStakingManager, ethStakingManager);
        EthStakingInstance.setVvvToken(address(VvvTokenInstance));
        EthStakingInstance.setNewStakesPermitted(true);
        vm.stopPrank();
    }

    function testScenario1_adminSetsHigherMultiplier() public {
        // sampleUser stakes 10 ether for 3 months (90 days)
        vm.prank(sampleUser);
        uint256 stakeId = EthStakingInstance.stakeEth{ value: 10 ether }(VVVETHStaking.StakingDuration.ThreeMonths);

        uint256[] memory stakingIds = EthStakingInstance.userStakeIds(sampleUser);
        assertEq(stakingIds.length, 1);

        // before 3 months, user can't withdraw their staked ethers
        vm.prank(sampleUser);
        vm.expectRevert();
        EthStakingInstance.withdrawStake(stakeId);

        // 3 months passes and user withdraw their ethers and claims their VVVTokens 
        advanceBlockNumberAndTimestampInSeconds(EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths) + 1);

        vm.prank(sampleUser);
        EthStakingInstance.withdrawStake(stakeId); // the user doesn't have any active stake

        // 10 VVV tokens accrued to user
        vm.prank(sampleUser);
        uint256 claimableVVVTokens = EthStakingInstance.calculateAccruedVvvAmount();
        assertEq(claimableVVVTokens, 10e18);

        // user claims that 10 VVV tokens
        vm.prank(sampleUser);
        EthStakingInstance.claimVvv(10e18);
        assertEq(VvvTokenInstance.balanceOf(sampleUser), 10e18); // user claimed their VVVs successfully

        // now user can't claim any VVVs (even 1wei)
        vm.prank(sampleUser);
        vm.expectRevert();
        EthStakingInstance.claimVvv(1);

        // admin decides to increase the multiplier (multiplier for 3_months is 10_000 and admin sets it to 20_000)
        vm.prank(ethStakingManager);

        VVVETHStaking.StakingDuration[] memory durations = new VVVETHStaking.StakingDuration[](1);
        durations[0] = VVVETHStaking.StakingDuration.ThreeMonths;

        uint256[] memory _multipliers = new uint256[](1);
        _multipliers[0] = 20_000;

        EthStakingInstance.setDurationMultipliers(durations, _multipliers);

        // now user can claim another 10 VVVs
        vm.prank(sampleUser);
        EthStakingInstance.claimVvv(10e18);
        assertEq(VvvTokenInstance.balanceOf(sampleUser), 20e18);

        // the user can repeat claiming until the admin sets higher values for multiplier
    }

    function testScenario2_adminSetsLowerMultiplier() public {
        // sampleUser stakes 10 ether for 1 year 
        vm.prank(sampleUser);
        uint256 stakeId = EthStakingInstance.stakeEth{ value: 10 ether }(VVVETHStaking.StakingDuration.OneYear);

        uint256[] memory stakingIds = EthStakingInstance.userStakeIds(sampleUser);
        assertEq(stakingIds.length, 1);

        // before 1 year , user can't withdraw their staked ethers
        vm.prank(sampleUser);
        vm.expectRevert();
        EthStakingInstance.withdrawStake(stakeId);

        // 1 year passes and user withdraws their ethers and claims their VVVTokens 
        advanceBlockNumberAndTimestampInSeconds(EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.OneYear) + 1);

        vm.prank(sampleUser);
        EthStakingInstance.withdrawStake(stakeId); // the user doesn't have any active stake

        // 30 VVV tokens accrued to user
        vm.prank(sampleUser);
        uint256 claimableVVVTokens = EthStakingInstance.calculateAccruedVvvAmount();
        assertEq(claimableVVVTokens, 30e18);

        // user claims that 30 VVV tokens
        vm.prank(sampleUser);
        EthStakingInstance.claimVvv(30e18);
        assertEq(VvvTokenInstance.balanceOf(sampleUser), 30e18); // user claimed their VVVs successfully

        // now user can't claim any VVVs (even 1wei)
        vm.prank(sampleUser);
        vm.expectRevert();
        EthStakingInstance.claimVvv(1);

        // admin decides to decrease the multiplier (multiplier for 1 year is 30_000 and admin sets it to 10_000)
        vm.prank(ethStakingManager);

        VVVETHStaking.StakingDuration[] memory durations = new VVVETHStaking.StakingDuration[](1);
        durations[0] = VVVETHStaking.StakingDuration.OneYear;

        uint256[] memory _multipliers = new uint256[](1);
        _multipliers[0] = 10_000;

        EthStakingInstance.setDurationMultipliers(durations, _multipliers);

        // user stakes 10 Ether again for 1 year (please note for the second year the multiplier is 10_000)
        vm.prank(sampleUser);
        stakeId = EthStakingInstance.stakeEth{ value: 10 ether }(VVVETHStaking.StakingDuration.OneYear);

        stakingIds = EthStakingInstance.userStakeIds(sampleUser);
        assertEq(stakingIds.length, 2);

        // 1 year passes
        advanceBlockNumberAndTimestampInSeconds(EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.OneYear) + 1);

        // user can't claim any VVV tokens (even 1 wei) and it underflows
        vm.prank(sampleUser);
        vm.expectRevert();
        EthStakingInstance.claimVvv(1);
    }

}

```
## Impact
setDurationMultipliers will cause 2 different issues for the protocol.
## Code Snippet
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L110-L116

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L264-L271

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L186

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L249-L251

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L201-L213

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L222-L241
## Tool used

Manual Review

## Recommendation
In my opinion, there should be added a new item inside the `StakeData` struct which stores the value of current `multiplier` (value of multiplier at time of staking) and the `accruedVVV` should be calculated based on that (this means the different stakes/positions should have different multipliers).

For example if the Alice stakes X Ethers today and today the multiplier is 10_000, and then tomorrow the Alice stakes Y Ethers and tomorrow's multiplier is 20_000, then for calculating the X's accruedVVV which should use 10_000 as multiplier and for calculating the Y's accruedVVV we should user 20_000 as multiplier.
something like this:
```diff
--- a/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol
+++ b/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol
@@ -38,6 +38,7 @@ contract VVVETHStaking is VVVAuthorizationRegistryChecker {
         uint32 stakeStartTimestamp;
         bool stakeIsWithdrawn;
         StakingDuration stakeDuration;
+        uint256 multiplier;
     }
```
```diff
@@ -234,7 +235,7 @@ contract VVVETHStaking is VVVAuthorizationRegistryChecker {
             nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

             accruedVvv =
-                (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
+                (nominalAccruedEth * ethToVvvExchangeRate() * _stake.multiplier) /
                 DENOMINATOR;
         }
```
```diff
@@ -295,7 +296,8 @@ contract VVVETHStaking is VVVAuthorizationRegistryChecker {
             stakedEthAmount: uint224(_stakedEthAmount),
             stakeStartTimestamp: uint32(block.timestamp),
             stakeIsWithdrawn: false,
-            stakeDuration: _stakeDuration
+            stakeDuration: _stakeDuration ,
+            multiplier: durationToMultiplier[_stakeDuration]
         });

         _userStakeIds[msg.sender].push(stakeId);
```