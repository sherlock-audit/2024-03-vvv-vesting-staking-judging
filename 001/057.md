Expert Daffodil Zebra

high

# Users won't be able to claim their tokens when _userStakeIds array becomes too big

## Summary
Users won't be able to claim their tokens when the `_userStakeIds` inevitably becomes too big.
## Vulnerability Detail
Let's take a look at the `claimVvv()` function:
```solidity
    function claimVvv(uint256 _vvvAmount) external {
        if (_vvvAmount == 0) revert CantClaimZeroVvv();

        uint256 claimableVvv = calculateClaimableVvvAmount();
        if (_vvvAmount > claimableVvv) revert InsufficientClaimableVvv();

        userVvvClaimed[msg.sender] += _vvvAmount;

        vvvToken.safeTransfer(msg.sender, _vvvAmount);

        emit VvvClaim(msg.sender, _vvvAmount);
    }
```
As you can see, `calculateClaimableVvvAmount()` is called:
```solidity
    function calculateClaimableVvvAmount() public view returns (uint256) {
        return calculateAccruedVvvAmount() - userVvvClaimed[msg.sender];
    }
```
which calls `calculateAccruedVvvAmount`:
```solidity
    function calculateAccruedVvvAmount() public view returns (uint256) {
        uint256[] memory stakeIds = _userStakeIds[msg.sender];
        if (stakeIds.length == 0) return 0;

        uint256 totalVvvAccrued;
        for (uint256 i = 0; i < stakeIds.length; ++i) {
            StakeData memory stake = userStakes[msg.sender][stakeIds[i]];
            unchecked {
                totalVvvAccrued += calculateAccruedVvvAmount(stake);
            }
        }

        return totalVvvAccrued;
    }
```
Which loops through each stakeId of the user and calculates the `totalVvvAccrued`. 

However, the problem is that the `_userStakeIds` is never decreased in length and will inevitably become too big for some users if they use the protocol in the long run. This will result in the transaction reverting and the user won't be able to claim his tokens. 

Currently, there is no restrictions on the amount of stakes a user can make, neither on a minimum stake amount. A long term user will use the protocol as intended and he'll reach a point where he has staked many times and he won't be able to claim his tokens because of out-of-gas revert.  
## Impact
Long term users won't be able to claim their tokens.
## Code Snippet
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L201-L214
## Tool used
Manual Review

## Recommendation
Keep a mapping of `totalVvvAccrued` for each user. There is no need to keep stakes that are withdrawn in the array so pop the stakes that are withdrawn in the `withdrawStake` function. Before that, calculate the accrued vvv amount and add it to the users mapping:
```solidity
valueAccruedFromStakeId = calculateAccruedVvvAmount(stakeId);
totalVvvAccrued[msg.sender] = totalVvvAccrued[msg.sender] + valueAccruedFromStakeId; 
```
Also, consider adding a reasonable minimum amount required for staking. 