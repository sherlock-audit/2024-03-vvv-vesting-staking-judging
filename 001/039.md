Zesty Powder Fox

medium

# `setDurationMultipliers` affects all stakings, so accrued amount of VVV token may differ from the amount calculated at staking moment.

## Summary
`setDurationMultipliers` affects `calculateAccruedVvvAmount` function because `calculateAccruedVvvAmount` function is only related to value of `durationMultiplier` at calling moment instead of value at staking moment, so accrued amount of VVV token may differ from the amount calculated at staking moment. 

## Vulnerability Detail
`calculateAccruedVvvAmount` is related to `durationToMultiplier[_stake.stakeDuration]` at calling moment but not staking moment.
```solidity
function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration
                ? stakeDuration
                : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

            accruedVvv =
                (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
                DENOMINATOR;
        }

        return accruedVvv;
    }
```
`setDurationMultipliers` updates `durationMultipliers` mapping storage variable globally without saving earlier value.
```solidity
function setDurationMultipliers(
        StakingDuration[] memory _duration,
        uint256[] memory _multipliers
    ) external onlyAuthorized { // @audit current staking 
        for (uint256 i = 0; i < _duration.length; ++i) {
            durationToMultiplier[_duration[i]] = _multipliers[i];
        }
    }
```
So `accruedVvvAmount` may differ from the amount calculated at the staking moment.

## Impact

- If `durationToMultiplier` value decreases from original value, an user who doesn't claim yet, can claim less than the amount calculated at the time of staking. It makes user loss of accrued amount of staking.
- If `durationToMultiplier` value increases from original value, even though an user have already fully claimed, he/she can claim additional amount because `calculateAccruedVvvAmount` function returns the bigger value than at earlier claimed moment. It results in protocol loss.
```solidity
additional accrued amount = stakedEth * (durationToMultiplierUpdated - durationToMultiplierOriginal)
```

## Code Snippet
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L230-L238
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L264-L271

## Tool used

Manual Review

## Recommendation
Add durationToMultiplier to StakeData struct definition, store durationToMultiplier at staking moment, and then calculate accrued vvv amount based on the value stored at staking moment.
```solidity
struct StakeData {
        uint224 stakedEthAmount;
        uint32 stakeStartTimestamp;
        bool stakeIsWithdrawn;
        StakingDuration stakeDuration;
++      uint256 durationToMultiplier;
    }

   function _stakeEth(StakingDuration _stakeDuration, uint256 _stakedEthAmount) private {
        if (_stakedEthAmount == 0) revert CantStakeZeroEth();
        ++stakeId;

        userStakes[msg.sender][stakeId] = StakeData({
            stakedEthAmount: uint224(_stakedEthAmount),
            stakeStartTimestamp: uint32(block.timestamp),
            stakeIsWithdrawn: false,
--          stakeDuration: _stakeDuration
++          stakeDuration: _stakeDuration,
++          durationToMultiplier: durationToMultiplier[_stakeDuration]
        });

        _userStakeIds[msg.sender].push(stakeId);

        emit Stake(
            msg.sender,
            stakeId,
            uint224(_stakedEthAmount),
            uint32(block.timestamp),
            _stakeDuration
        );
    }

function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;
        uint256 multiplier = _stake.durationMultiplier;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration
                ? stakeDuration
                : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

            accruedVvv =
--                (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
++                (nominalAccruedEth * ethToVvvExchangeRate() * _stake.durationToMultiplier) /
                DENOMINATOR;
        }

        return accruedVvv;
    }
```