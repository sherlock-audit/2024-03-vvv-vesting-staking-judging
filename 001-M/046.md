Swift Saffron Snake

high

# `VVVETHStaking::setDurationMultipliers` will disrupt the ongoing staking activity, causing unfair profits or losses.

## Summary

The function `setDurationMultipliers` within the `VVVETHStaking` contract is aimed to update the multipliers for each duration allowed to stake ETH (3 months, 6 months, and 1 year). These multipliers are used to calculate the accrued `VVV` tokens from staking ETH.

When updating these multipliers, the calculation for accrued tokens will take the new multiplier value as if it was since the start so it will cause unfair profits or losses to users that have active staking. 

## Vulnerability Detail

The function `setDurationMultipliers` updates the multipliers for each staking duration:

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L264-L271
```solidity
function setDurationMultipliers(
    StakingDuration[] memory _duration,
    uint256[] memory _multipliers
) external onlyAuthorized {
    for (uint256 i = 0; i < _duration.length; ++i) {
        durationToMultiplier[_duration[i]] = _multipliers[i];
    }
}
```

These multipliers are used within the function `calculateAccruedVvvAmount`:

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L237
```solidity
    function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration
                ? stakeDuration
                : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

            accruedVvv =
>>>             (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
                DENOMINATOR;
        }

        return accruedVvv;
    }
```

When the value of the multipliers is changed via the `setDurationMultipliers` function, the calculations for accrued `VVV` tokens will be wrong because it will take the new value of the multiplier as if it were since the start.

When the duration multiplier is updated to a higher value, all users that have had some ETH staked for that duration they will have new `VVV` tokens to claim via `claimVvv()`. Even users that had some staking in the past and have already withdrawn it and claimed all the tokens, when the multiplier increases, will be able to claim even more tokens.

When the multiplier is updated to a lower value, users may experience losses or even a DoS on `claimVvv()`. That is because the function `calculateAccruedVvvAmount` will return a lower value than previously, and therefore it will result in an underflow that will prevent users from claiming more `VVV` tokens. 

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L249-L251
```solidity
function calculateClaimableVvvAmount() public view returns (uint256) {
    return calculateAccruedVvvAmount() - userVvvClaimed[msg.sender];
}
```

## Impact

When any multiplier is changed to a higher value, all users who had some stakes in the past or the present will be able to claim even more `VVV` tokens.

When a multiplier is changed to a lower value, users will lose some unclaimed tokens, and they will even experience a DoS on `claimVvv` due to underflow.

## PoC

The following tests can be pasted in `VVVETHStaking.unit.t.sol` and be run with the following commands:
 - `forge test --match-test test_increase_multipliers`
 - `forge test --match-test test_decrease_multipliers`

```solidity
function test_increase_multipliers() public {
    vm.startPrank(sampleUser, sampleUser);
    uint256 stakeEthAmount = 1 ether;
    EthStakingInstance.stakeEth{ value: stakeEthAmount }(VVVETHStaking.StakingDuration.ThreeMonths);

    // forward to half the staking duration
    advanceBlockNumberAndTimestampInSeconds(
        EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths) / 2 + 1
    );

    // Claim the accrued VVV
    uint256 claimableVvv = EthStakingInstance.calculateClaimableVvvAmount();
    EthStakingInstance.claimVvv(claimableVvv);
    vm.stopPrank();

    // Duration multipliers are changed to a higher value
    VVVETHStaking.StakingDuration[] memory durations = new VVVETHStaking.StakingDuration[](1);
    uint256[] memory multipliers = new uint256[](1);
    durations[0] = VVVETHStaking.StakingDuration.ThreeMonths;
    multipliers[0] = 20_000;

    vm.prank(ethStakingManager, ethStakingManager);
    EthStakingInstance.setDurationMultipliers(durations, multipliers);

    // Now, the user will claim even more VVV tokens
    vm.startPrank(sampleUser, sampleUser);
    uint256 claimableVvvAfterMultiplierUpdate = EthStakingInstance.calculateClaimableVvvAmount();
    EthStakingInstance.claimVvv(claimableVvvAfterMultiplierUpdate);

    // Because the duration multiplier have doubled, now we can claim more VVV tokens
    assertEq(claimableVvvAfterMultiplierUpdate, claimableVvv);

    vm.stopPrank();
}

function test_decrease_multipliers() public {
    vm.startPrank(sampleUser, sampleUser);
    uint256 stakeEthAmount = 1 ether;
    EthStakingInstance.stakeEth{ value: stakeEthAmount }(VVVETHStaking.StakingDuration.ThreeMonths);

    // forward to half the staking duration
    advanceBlockNumberAndTimestampInSeconds(
        EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths) / 2 + 1
    );

    // Claim the accrued VVV
    uint256 claimableVvv = EthStakingInstance.calculateClaimableVvvAmount();
    EthStakingInstance.claimVvv(claimableVvv);
    vm.stopPrank();

    // Duration multipliers are changed to a lower value
    VVVETHStaking.StakingDuration[] memory durations = new VVVETHStaking.StakingDuration[](1);
    uint256[] memory multipliers = new uint256[](1);
    durations[0] = VVVETHStaking.StakingDuration.ThreeMonths;
    multipliers[0] = 5_000;

    vm.prank(ethStakingManager, ethStakingManager);
    EthStakingInstance.setDurationMultipliers(durations, multipliers);

    // After 1 day...
    vm.warp(1 days);
    vm.roll(1 days);

    // Now, the user will experience a DoS on claiming tokens
    vm.startPrank(sampleUser, sampleUser);
    vm.expectRevert();
    uint256 claimableVvvAfterMultiplierUpdate = EthStakingInstance.calculateClaimableVvvAmount();
    vm.expectRevert();
    EthStakingInstance.claimVvv(claimableVvvAfterMultiplierUpdate);

    vm.stopPrank();
}
```

## Code Snippet

Function `setDurationMultipliers`: https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L264-L271

## Tool used

Manual Review

## Recommendation

To mitigate this issue is recommended to not allow admins to ever change the multipliers because that will result in a disruption of the current and previous staking activity. 

To be able to change the current multipliers, the architecture of the `VVVETHStaking` should adapt, implementing an index-based reward system. 
