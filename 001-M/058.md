Formal Vermilion Coyote

medium

# The current multiplier should be stored in the StakeData structure

## Summary

`claimVvv` internally calls [[calculateClaimableVvvAmount()](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L186)](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L186) to calculate the claimable amount for caller. The most critical variable that affects the results is `durationToMultiplier` mapping, which records the accrual multiplier for 3 durations: ThreeMonths, SixMonths, OneYear.  
Administrator can modify the multiplier of each duration via [[setDurationMultipliers](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L264)](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L264). The problem is that [[calculateClaimableVvvAmount() relies on the value in the current durationToMultiplier mapping](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L237)](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L237), not the value at stake. This causes the results returned by `calculateClaimableVvvAmount()` to be incorrect.

## Vulnerability Detail

The flow of [[claimVvv](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L183-L194)](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L183-L194) is as follows:

```flow
claimVvv
  calculateClaimableVvvAmount()
    calculateAccruedVvvAmount()
      uint256[] memory stakeIds = _userStakeIds[msg.sender];
      if (stakeIds.length == 0) return 0;
      uint256 totalVvvAccrued;
      for (uint256 i = 0; i < stakeIds.length; ++i) {
        StakeData memory stake = userStakes[msg.sender][stakeIds[i]];
          unchecked {
->          totalVvvAccrued += calculateAccruedVvvAmount(stake);
          }
      }
      return totalVvvAccrued;
```

[[calculateAccruedVvvAmount()](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L201-L214)](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L201-L214) will traverse `_userStakeIds[msg.sender]` array and calculate the total amount of $VVV accrued for each single stake. The items in the `_userStakeIds[msg.sender]` array will exist forever and will not be popped.

Let's look at the code for [[calculateAccruedVvvAmount(stake)](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L222)](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L222):

```solidity
File: vvv-platform-smart-contracts\contracts\staking\VVVETHStaking.sol
222:     function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
223:         uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
224:         uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
225:         uint256 secondsStaked;
226:         uint256 nominalAccruedEth;
227:         uint256 accruedVvv;
228: 
229:         unchecked {
230:             secondsStaked = secondsSinceStakingStarted >= stakeDuration
231:                 ? stakeDuration
232:                 : secondsSinceStakingStarted;
233: 
234:             nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;
235: 
236:             accruedVvv =
237:->               (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
238:                 DENOMINATOR;
239:         }
240: 
241:         return accruedVvv;
242:     }
```

Let us take an example to briefly describe this issue:

`durationToMultiplier[StakingDuration.ThreeMonths] = 10_000`

1.  bob stakes 10 ether via `stakeEth(StakingDuration.ThreeMonths)`.
2.  Three months later, bob got back the 10 ether via [[withdrawStake](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L162)](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L162), and received 10e18 VVVToken via `claimVvv(10e18)`. At this point, bob has completely exited.
3.  The administrator modifies `durationToMultiplier[StakingDuration.ThreeMonths]` to 20_000 via `setDurationMultipliers`.
4.  bob can still claim another 10e18 VVVToken via `claimVvv`.

bob is out of the game and shouldn't get the extra 10e18 VVVToken. **Modified parameters should not take effect on previous stake data**. Here, we only describe the scenario where the multiplier is increased. Similarly, if the multiplier is reduced, the user will not receive the rewards he deserves.

## Impact

- When the multiplier is increased, users can claim additional rewards for expired stakes. This is a loss of funds for the protocol.
- When the multiplier is reduced, users cannot claim the rewards they deserve for ongoing staking. This is a loss of funds for the user.

## Code Snippet

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L237

## Tool used

Manual Review

## Recommendation

It is recommended to add a multiplier to StakeData:

```solidity
struct StakeData {
        uint224 stakedEthAmount;
        uint32 stakeStartTimestamp;
        bool stakeIsWithdrawn;
        StakingDuration stakeDuration;
->      uint256 multiplier;
    }
```

Assign a value to the multiplier in `_stakeEth`:

```fix
File: vvv-platform-smart-contracts\contracts\staking\VVVETHStaking.sol
290:     function _stakeEth(StakingDuration _stakeDuration, uint256 _stakedEthAmount) private {
......
294:         userStakes[msg.sender][stakeId] = StakeData({
295:             stakedEthAmount: uint224(_stakedEthAmount),
296:             stakeStartTimestamp: uint32(block.timestamp),
297:             stakeIsWithdrawn: false,
298:             stakeDuration: _stakeDuration,
    +++          multiplier: durationToMultiplier[_stakeDuration]
299:         });
......
310:     }
```

And modify the multiplier used in `calculateAccruedVvvAmount`:

```fix
File: vvv-platform-smart-contracts\contracts\staking\VVVETHStaking.sol
222:     function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
......
236:             accruedVvv =
237:---              (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
237:+++              (nominalAccruedEth * ethToVvvExchangeRate() * _stake.multiplier) /
238:                 DENOMINATOR;
239:         }
......
242:     }
```

&nbsp;