Broad Cornflower Iguana

high

# Duration multipliers update affects all historical users stakes

## Summary

The `VVVETHStaking` allows users to make multiple stakes and accrue VVV token over time as a staking reward. The protocol owner can update duration multipliers any time. It was identified that updated duration multipliers affect all historical users stakes giving stakers additional rewards or preventing them claiming any additional rewards. 

The `setDurationMultipliers()` is protected with the `onlyAuthorized` modifier, so it can be called only by `TRUSTED` account. However, the implications of this function usage impact the whole protocol, as the design is defected. 

## Vulnerability Detail

The `setDurationMultipliers()` can be called by the privileged user any time to update duration multipliers. 

```solidity
    function setDurationMultipliers(
        StakingDuration[] memory _duration,
        uint256[] memory _multipliers
    ) external onlyAuthorized {
        for (uint256 i = 0; i < _duration.length; ++i) {
            durationToMultiplier[_duration[i]] = _multipliers[i];
        }
    }
```

The `calculateAccruedVvvAmount()` is used to calculate the total amount of accrued VVV tokens since the beginning of the stake.

```solidity
    function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration
                ? stakeDuration
                : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

            accruedVvv =
                (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
                DENOMINATOR;
        }

        return accruedVvv;
    }
```

The `calculateClaimableVvvAmount()` subtracts the value returned by the `calculateAccruedVvvAmount()` function and the value from the `userVvvClaimed` mapping record. Thus, this function allows to precisely provide amount of accrued but not yet claimed tokens.

```solidity
    function calculateClaimableVvvAmount() public view returns (uint256) {
        return calculateAccruedVvvAmount() - userVvvClaimed[msg.sender];
    }
```

The `userVvvClaimed` mapping record is updated every time the `claimVvv()` function is called. Thus, this function allows to claim tokens any time and it prevents claiming the excessed amount of tokens. 

```solidity
    function claimVvv(uint256 _vvvAmount) external {
        if (_vvvAmount == 0) revert CantClaimZeroVvv();

        uint256 claimableVvv = calculateClaimableVvvAmount();
        if (_vvvAmount > claimableVvv) revert InsufficientClaimableVvv();

        userVvvClaimed[msg.sender] += _vvvAmount;

        vvvToken.safeTransfer(msg.sender, _vvvAmount);

        emit VvvClaim(msg.sender, _vvvAmount);
    }
```

However, the `userVvvClaimed` mapping record holds accumulated value of claimed tokens calculated by means of `durationToMultiplier`. If the `durationToMultiplier` is updated, the return value of the `calculateAccruedVvvAmount()` function is updated as well, but  the `userVvvClaimed` mapping record remains with previous, historical value.

This design flaw leads to two scenarios:
1) If `durationToMultiplier` is decreased the subsequent call to the `claimVvv()` may revert due to arithmetic underflow as value hold by the `userVvvClaimed` can be higher than returned by the `calculateAccruedVvvAmount()` function in calculation: `calculateAccruedVvvAmount() - userVvvClaimed[msg.sender];`.
2) if `durationToMultiplier` is increased the user can leverage from this by accruing additional amount of tokens by calling the `claimVvv()`, as value hold by the `userVvvClaimed` can be lower than returned by the `calculateAccruedVvvAmount()` function in calculation: `calculateAccruedVvvAmount() - userVvvClaimed[msg.sender];`.

## Impact

The duration multiplier affects all historical stakings. After an update ann user may be prevented claiming accrued tokens or may be granted additional amount of tokens to claim.

## Proof of Concept

```solidity
function test_claimVvvv_then_decrease_stakingDuration() public {
        vm.startPrank(sampleUser, sampleUser);
        uint256 stakeEthAmount = 1 ether;
        EthStakingInstance.stakeEth{ value: stakeEthAmount }(VVVETHStaking.StakingDuration.ThreeMonths);

        // forward to first timestamp with released stake
        advanceBlockNumberAndTimestampInSeconds(
            EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths)/2 + 1
        );

        uint256 claimableVvv = EthStakingInstance.calculateClaimableVvvAmount();

        uint256 vvvBalanceBefore = VvvTokenInstance.balanceOf(sampleUser);

        EthStakingInstance.claimVvv(claimableVvv);
        uint256 vvvBalanceAfter = VvvTokenInstance.balanceOf(sampleUser);
        vm.stopPrank();

        vm.startPrank(ethStakingManager, ethStakingManager);
        VVVETHStaking.StakingDuration[] memory durations = new VVVETHStaking.StakingDuration[](3);
        durations[0] = VVVETHStaking.StakingDuration.ThreeMonths;
        durations[1] = VVVETHStaking.StakingDuration.SixMonths;
        durations[2] = VVVETHStaking.StakingDuration.OneYear;

        uint256[] memory multipliers = new uint256[](3);
        multipliers[0] = 5_000;
        multipliers[1] = 10_000;
        multipliers[2] = 15_000;

        EthStakingInstance.setDurationMultipliers(durations, multipliers);
        vm.stopPrank();

        vm.prank(sampleUser, sampleUser);
        vm.expectRevert(stdError.arithmeticError); //@audit note revert due to arithmetic underflow here
        EthStakingInstance.claimVvv(claimableVvv);
    }

    function test_claimVvvv_then_increase_stakingDuration() public {
        vm.startPrank(sampleUser, sampleUser);
        uint256 stakeEthAmount = 1 ether;
        EthStakingInstance.stakeEth{ value: stakeEthAmount }(VVVETHStaking.StakingDuration.ThreeMonths);

        // forward to first timestamp with released stake
        advanceBlockNumberAndTimestampInSeconds(
            EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths)/2 + 1
        );

        uint256 claimableVvv = EthStakingInstance.calculateClaimableVvvAmount();

        uint256 vvvBalanceBefore = VvvTokenInstance.balanceOf(sampleUser);
        assertEq(vvvBalanceBefore, 0);

        EthStakingInstance.claimVvv(claimableVvv);

        uint256 vvvBalanceAfter = VvvTokenInstance.balanceOf(sampleUser);
        assertEq(vvvBalanceAfter, 5*1e17);

        vm.expectRevert(VVVETHStaking.InsufficientClaimableVvv.selector);
        EthStakingInstance.claimVvv(claimableVvv);
        vm.stopPrank();

        vm.startPrank(ethStakingManager, ethStakingManager);
        VVVETHStaking.StakingDuration[] memory durations = new VVVETHStaking.StakingDuration[](3);
        durations[0] = VVVETHStaking.StakingDuration.ThreeMonths;
        durations[1] = VVVETHStaking.StakingDuration.SixMonths;
        durations[2] = VVVETHStaking.StakingDuration.OneYear;

        uint256[] memory multipliers = new uint256[](3);
        multipliers[0] = 20_000;
        multipliers[1] = 25_000;
        multipliers[2] = 30_000;

        EthStakingInstance.setDurationMultipliers(durations, multipliers);
        vm.stopPrank();

        vm.startPrank(sampleUser, sampleUser);
        EthStakingInstance.claimVvv(claimableVvv);
        vvvBalanceAfter = VvvTokenInstance.balanceOf(sampleUser);
        assertEq(vvvBalanceAfter, 2 * 5*1e17); //@audit note double reward here
        vm.stopPrank();
    }
```

## Code Snippet

[VVVETHStaking.sol#L237](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L237)

[VVVETHStaking.sol#L189](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L189)

[VVVETHStaking.sol#L250](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L250)

## Tool used

Manual Review

## Recommendation

It is recommended to either remove the possibility of updating the duration multipliers or change the design of the protocol so an updated does not affect historical users stakes.
