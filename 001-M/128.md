Amateur Wool Camel

medium

# Change in `VVVETHStaking::durationToMultiplier` will lead to incorrect calculation of rewards.

## Summary

`durationToMultiplier` is the multiplier based on duration of eth staked, and it is used to calculate the VVV token reward at the time of claiming VVV tokens which result in uncentainty in the rewards quantity.

## Vulnerability Detail

1. The current `durationToMultiplier` for `OneYear` is 30_000.
2. User stake 100 Eth for One year duration.
3. After One year user expect to get 300 VVV tokens as the `durationToMultiplier` for one year is 1 and 30_000.
4. But the current `durationToMultiplier` at the time of staking is not begin store anywhere.
5. In the mean time the ADMIN decided to change the `durationToMultiplier` for one year to 10_000 based on market demands.
6. After one year, when the user claim their VVV tokens they will get 100 VVV tokens instead of 300.

- Run this test in `VVVETHStaking.unit.t.sol`

```javascript
    function testUserGetsIncorrectRewardAsDurationMultipliersChanges() public {
        // user stakes 100 ethers
        address USER = makeAddr("user");
        uint256 startingBalance = 100 ether;
        vm.deal(USER, startingBalance);
        vm.startPrank(USER);
        EthStakingInstance.stakeEth{value: startingBalance}(VVVETHStaking.StakingDuration.OneYear);

        // one year passed
        vm.warp(1641070800102930918230912);
        vm.roll(100);

        uint256 expectedReward = EthStakingInstance.calculateAccruedVvvAmount();
        vm.stopPrank();

        // ADMIN changed the interest rates
        vm.startPrank(ethStakingManager);
        VVVETHStaking.StakingDuration[] memory durations = new VVVETHStaking.StakingDuration[](3);
        durations[0] = VVVETHStaking.StakingDuration.ThreeMonths;
        durations[1] = VVVETHStaking.StakingDuration.SixMonths;
        durations[2] = VVVETHStaking.StakingDuration.OneYear;

        uint256[] memory multipliers = new uint256[](3);
        multipliers[0] = 5_000;
        multipliers[1] = 7_500;
        multipliers[2] = 10_000;
        EthStakingInstance.setDurationMultipliers(durations, multipliers);
        vm.stopPrank();

        // after one year user claim their rewards
        vm.startPrank(USER);
        uint256 actualReward = EthStakingInstance.calculateAccruedVvvAmount();
        vm.stopPrank();

        console.log("expected reward: ", expectedReward);
        console.log("actual reward: ", actualReward);

        assert(expectedReward == 300 ether);
        assert(actualReward == 100 ether);
        assert(expectedReward != actualReward);
    }
```

## Impact

- Users getting incorrect VVV token rewards for staking

## Code Snippet

Github:- https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/2d3540ab8f421055dba60d47e948e1cc55f0a0c7/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L237

```javascript
    function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration
                ? stakeDuration
                : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

@>          accruedVvv =
                (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
                DENOMINATOR;
        }

        return accruedVvv;
    }
```

## Tool used

Manual Review

## Recommendation

- store the `durationToMultiplier` at the time of staking in the `struct StakeData` of the user and chagne the entire contract accordingly
- use this `durationToMultiplier` that is store in the `struct StakeData` to calculate the rewards.

```diff
    struct StakeData {
        uint224 stakedEthAmount;
        uint32 stakeStartTimestamp;
        bool stakeIsWithdrawn;
+       uint256 multiplier;
        StakingDuration stakeDuration;
    }
    .
    .
    .
    function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration ? stakeDuration : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;
-           accruedVvv =
-               (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) / DENOMINATOR;
+           accruedVvv = (nominalAccruedEth * ethToVvvExchangeRate() * _stake.multiplier) / DENOMINATOR;
        }

        return accruedVvv;
    }
    .
    .
    .
    function _stakeEth(StakingDuration _stakeDuration, uint256 _stakedEthAmount) private {
        if (_stakedEthAmount == 0) revert CantStakeZeroEth();
        ++stakeId;

        userStakes[msg.sender][stakeId] = StakeData({
            stakedEthAmount: uint224(_stakedEthAmount),
            stakeStartTimestamp: uint32(block.timestamp),
            stakeIsWithdrawn: false,
+           multiplier: durationToMultiplier[_stakeDuration],
            stakeDuration: _stakeDuration
        });

        _userStakeIds[msg.sender].push(stakeId);

        emit Stake(msg.sender, stakeId, uint224(_stakedEthAmount), uint32(block.timestamp), _stakeDuration);
    }
```

