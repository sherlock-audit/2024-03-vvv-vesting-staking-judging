Main Cotton Mandrill

high

# Modifying DurationMultipliers can cause users to lose his reward already collected

[H1] Modifying DurationMultipliers can cause users to lose his reward already collected.

## Summary
When an admin decreases the DurationMultipliers, a user who has already collected rewards and decides to claim later may lose some of their rewards.

## Vulnerability Detail
A user who stakes 1 Ether for 3 months, then waits 3 months to claim their reward in two steps, may lose a portion of their rewards if the admin decreases the DurationMultipliers between their two claims.

## Impact
- Users lose a portion of their rewards if the admin decreases the DurationMultipliers betwwen theirs 2 claims.
- Two users who stake at the same time for the same duration will not receive the same reward if one claims before the DurationMultipliers change and the other claims just after.(even in the same block)
- Users' rewards will be blocked if the admin decides to set DurationMultipliers to 0, even if the users have already collected the reward and restaked multiple times for a long period.

## Code Snippet/POC

[https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L237](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L237)

Add this test code to the VVVETHStakingUnitTests contract:

```javascript
    //We will test the user keep all his reward already collected even if the admin change the duration multiplier
    function testClaimLostFundWhenChangeDurationMultipliers() public {
        vm.startPrank(sampleUser, sampleUser);
        uint256 stakeEthAmount = 1 ether;
        //user stake 1 ETH
        EthStakingInstance.stakeEth{value: stakeEthAmount}(VVVETHStaking.StakingDuration.ThreeMonths);

        //forward staking duration / stakingDurationDivisor for 3 months
        advanceBlockNumberAndTimestampInSeconds(
            EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths)
        );
    
        uint256 claimableVvv = EthStakingInstance.calculateClaimableVvvAmount();

        //user claim half of his reward immediately
        EthStakingInstance.claimVvv(claimableVvv / 2);

        //user really get the half of his reward
        uint256 vvvBalanceBefore = VvvTokenInstance.balanceOf(sampleUser);
        assertEq(vvvBalanceBefore, claimableVvv / 2);
        vm.stopPrank();

        //The admin change change the duration multiplier to something lower for futur staking
        vm.startPrank(ethStakingManager, ethStakingManager);
        VVVETHStaking.StakingDuration[] memory durations = new VVVETHStaking.StakingDuration[](3);
        durations[0] = VVVETHStaking.StakingDuration.ThreeMonths;
        durations[1] = VVVETHStaking.StakingDuration.SixMonths;
        durations[2] = VVVETHStaking.StakingDuration.OneYear;

        uint256[] memory multipliers = new uint256[](3);
        multipliers[0] = 5_000;
        multipliers[1] = 7_000;
        multipliers[2] = 10_000;

        EthStakingInstance.setDurationMultipliers(durations, multipliers);

        vm.stopPrank();

        //user decide to claim the rest of his reward 0.001 second after his first claim
        vm.startPrank(sampleUser, sampleUser);
        EthStakingInstance.claimVvv(claimableVvv / 2);

        // user should get the remaining half of reward , but he get an error because of InsufficientClaimableVvv
        uint256 vvvAfterBefore = VvvTokenInstance.balanceOf(sampleUser);
        assertEq(vvvAfterBefore, claimableVvv);

        vm.stopPrank();
    }
```

Run the command
```shell
forge test --mt testClaimLostFundWhenChangeDurationMultipliers
```
The result is:
```shell
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 24.17ms (16.68ms CPU time)

Ran 1 test suite in 1.06s (24.17ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/staking/VVVETHStaking.unit.t.sol:VVVETHStakingUnitTests
[FAIL. Reason: InsufficientClaimableVvv()] testClaimLostFundWhenChangeDurationMultipliers() (gas: 266155)

Encountered a total of 1 failing tests, 0 tests succeeded
```

## Tool used
Manual Review

## Recommendation
In the VVVETHStaking contract, a new field should be added to StakeData to store the durationToMultiplier corresponding to the StakingDuration chosen by the user, when he stakes.

Thus, when the user stakes, this variable is updated and will be used to calculate the claim.
```diff
    struct StakeData {
        uint224 stakedEthAmount;
        uint32 stakeStartTimestamp;
        bool stakeIsWithdrawn;
        StakingDuration stakeDuration;
+       uint256 durationToMultiplier;
    }
```
Change the function calculateAccruedVvvAmount to replace durationToMultiplier[_stake.stakeDuration]) by _stake.durationToMultiplier.
Update the function _stakeEth to save  durationToMultiplier  into StakeData

```javascript
    function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration ? stakeDuration : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

            accruedVvv =
@>               (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) / DENOMINATOR;
        }

        return accruedVvv;
    }
```
```javascript
    function _stakeEth(StakingDuration _stakeDuration, uint256 _stakedEthAmount) private {
        if (_stakedEthAmount == 0) revert CantStakeZeroEth();
        ++stakeId;

        userStakes[msg.sender][stakeId] = StakeData({ 
            stakedEthAmount: uint224(_stakedEthAmount), //@audit check if could have an overflow here
            stakeStartTimestamp: uint32(block.timestamp),
            stakeIsWithdrawn: false,
            stakeDuration: _stakeDuration
@>
        });

        _userStakeIds[msg.sender].push(stakeId); 

        emit Stake(msg.sender, stakeId, uint224(_stakedEthAmount), uint32(block.timestamp), _stakeDuration);
    }
```

The DurationMultipliers variable should be carefully designed and tested to avoid unintended consequences.
Users rewards already collected should'nt impacted of any changes to the DurationMultipliers variable.