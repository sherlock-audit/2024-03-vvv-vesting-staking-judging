Oblong Cedar Rooster

medium

# Short-term stakers always gain more rewards than long-term stakers via restocking

## Summary

Due to how the multipliers are designed, short-term stakers can gain more rewards than long-term stakers, leading to long-term stakers losing out on more rewards than short-term stakers.

## Vulnerability Detail

The following multipliers are:

- 3 month stakers are provided a 100% reward. 
- 6 month stakers are provided a 150% reward.
- 12 month stakers are provided a 300% reward.

Since stakers are allowed to restake their rewards, a short term staker can restake every 3 months, gaining 100% rewards every 3 months. This allows a 3-month staker to gain 400% in rewards by restaking 3 times in a row versus the 12-month staker who will only make 300% in rewards. Additionally, if a short-term staker wanted, they can restake twice (totaling 9 months of staking) and receive the same amount of rewards as a staker who staked for a full year.

Below is a forge test which shows how short-term stakers gain more rewards than the long-term stakers:

```solidity

function test_RestakeAndClaimBonuses() public {
    VVVETHStaking.StakingDuration restakeDuration = VVVETHStaking.StakingDuration.ThreeMonths;
    uint256 restakeId;
    vm.startPrank(sampleUser, sampleUser);
    uint256 stakeEthAmount = 1 ether;
    uint256 vvvBalanceBefore = VvvTokenInstance.balanceOf(sampleUser);

    // Simulating short restaking 4 times...

    uint stakeId = EthStakingInstance.stakeEth{ value: stakeEthAmount }(restakeDuration);
    advanceBlockNumberAndTimestampInSeconds(
        EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths) + 1
    );

    restakeId = EthStakingInstance.restakeEth(stakeId, restakeDuration);
    advanceBlockNumberAndTimestampInSeconds(
        EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths) + 1
    );

    restakeId = EthStakingInstance.restakeEth(restakeId, restakeDuration);
    advanceBlockNumberAndTimestampInSeconds(
        EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths) + 1
    );

    restakeId = EthStakingInstance.restakeEth(restakeId, restakeDuration);
    advanceBlockNumberAndTimestampInSeconds(
        EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths) + 1
    );

    uint256 claimableVvv = EthStakingInstance.calculateClaimableVvvAmount();

    console.log("claimableVvv: ", claimableVvv);
    EthStakingInstance.claimVvv(claimableVvv);
    uint256 vvvBalanceAfter = VvvTokenInstance.balanceOf(sampleUser);

    uint valueReceivedThroughShortRestaking = vvvBalanceAfter - vvvBalanceBefore;

    // Now let's simulate a year long stake...

    EthStakingInstance.stakeEth{ value: stakeEthAmount }(VVVETHStaking.StakingDuration.OneYear);
    advanceBlockNumberAndTimestampInSeconds(
        EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.OneYear) + 1
    );
    claimableVvv = EthStakingInstance.calculateClaimableVvvAmount();
    EthStakingInstance.claimVvv(claimableVvv);

    uint256 vvvBalanceAfterOneYear = VvvTokenInstance.balanceOf(sampleUser);

    uint valueReceivedThroughLongTermRestaking = vvvBalanceAfterOneYear - vvvBalanceAfter;

    // AUDIT: short term restaking always gains more rewards than long term restaking.
    assertEq(true, valueReceivedThroughLongTermRestaking < valueReceivedThroughShortRestaking);

    vm.stopPrank();
}
```

## Impact

Long-term stakers will be lose out on rewards vs. a short-term staker. Not only do short-term stakers gain more rewards than long-term rewards, long-term stakers maintain more risk as their tokens are held longer, while short-term stakers can withdraw their stake in a shorter time-period.  

## Code Snippet

https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol?plain=1#L114-L116

## Tool used

Manual Review

## Recommendation

The duration multipliers should be adjusted such that long-term stakers should receive more rewards than short-term stakers. For example...

```solidity
durationToMultiplier[StakingDuration.ThreeMonths] = 5_000;
durationToMultiplier[StakingDuration.SixMonths] = 12_000;
durationToMultiplier[StakingDuration.OneYear] = 30_000;
```
