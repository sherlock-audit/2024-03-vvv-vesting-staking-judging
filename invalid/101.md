Magnificent Grey Sawfish

medium

# Division Before Multiplication computes lesser reward

## Summary
contract `VVVETHStaking.sol` performs multiplication on result of a division while computing value of `accruedVvv` which will cause loss of precision and loss of funds to users/ protocol.

## Vulnerability Detail
```solidity
File: contracts/staking/VVVETHStaking.sol

/// @audit ******************* Issue Detail *******************
VVVETHStaking.calculateAccruedVvvAmount(VVVETHStaking.StakeData) (contracts/staking/VVVETHStaking.sol#222-242) performs a multiplication on the result of a division:
	- nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration (contracts/staking/VVVETHStaking.sol#234)
	- accruedVvv = (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) / DENOMINATOR (contracts/staking/VVVETHStaking.sol#236-238)

/// @audit ************** Possible Issue Line(s) **************
	L#234,  L#236-238,  

/// @audit ****************** Affected Code *******************
 222:     function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
 223:         uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
 224:         uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
 225:         uint256 secondsStaked;
 226:         uint256 nominalAccruedEth;
 227:         uint256 accruedVvv;
 228: 
 229:         unchecked {
 230:             secondsStaked = secondsSinceStakingStarted >= stakeDuration
 231:                 ? stakeDuration
 232:                 : secondsSinceStakingStarted;
 233: 
 234:             nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;
 235: 
 236:             accruedVvv =
 237:                 (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
 238:                 DENOMINATOR;
 239:         }
 240: 
 241:         return accruedVvv;
 242:     }
```
## Impact
protocol will compute lesser `accruedVvv` value and user will get lesser rewards.
Consider following simple PoC code. The code also implements an optimized version of `calculateAccruedVvvAmount` function which avoid multiplication on result of a division. The results of `accruedVvv` (for duration of one to fifteen months) when `test` function is called are as follows (the difference in both values show the reward loss caused to user over different staked periods):-

```solidity
// PoC Output
calculateAccruedVvvAmount - uint256[]: 0,249,498,750,999,1248,1500,1749,1998,2250,2499,2748,3000,3000,3000,3000
calculateAccruedVvvAmount_optimized: uint256[]: 0,250,500,750,1000,1250,1500,1750,2000,2250,2500,2750,3000,3000,3000,3000
```

**The PoC Code**
```solidity
//simplified contract to highlight issue in calculateAccruedVvvAmount
pragma solidity >=0.6.12 <0.9.0;

contract VVVETHStaking {
    struct StakeData {
        uint224 stakedEthAmount;
        uint32 stakeStartTimestamp;
        bool stakeIsWithdrawn;
        StakingDuration stakeDuration;
    }

    enum StakingDuration {
        ThreeMonths,
        SixMonths,
        OneYear
    }

    uint256 public constant DENOMINATOR = 10_000;

    mapping(StakingDuration => uint256) public durationToSeconds;

    mapping(StakingDuration => uint256) public durationToMultiplier;

    constructor() {
        durationToSeconds[StakingDuration.ThreeMonths] = 90 days;
        durationToSeconds[StakingDuration.SixMonths] = 180 days;
        durationToSeconds[StakingDuration.OneYear] = 360 days;

        durationToMultiplier[StakingDuration.ThreeMonths] = 10_000;
        durationToMultiplier[StakingDuration.SixMonths] = 15_000;
        durationToMultiplier[StakingDuration.OneYear] = 30_000;
    }

    function ethToVvvExchangeRate() public pure returns (uint256) {
        return 1;
    }

    function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration
                ? stakeDuration
                : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

            accruedVvv =
                (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
                DENOMINATOR;
        }

        return accruedVvv;
    }

    // multiplication on result of division has been avoided by
    // re-arranging the computations 
    function calculateAccruedVvvAmount_Optimized(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration
                ? stakeDuration
                : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount);

            accruedVvv =
                (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
                stakeDuration /
                DENOMINATOR;
        }

        return accruedVvv;
    }

    //test function to compte accruedVvv for both 
    ///calculateAccruedVvvAmount and calculateAccruedVvvAmount_Optimized
    function test() public view returns (uint256[] memory, uint256[] memory) {
        uint256[] memory accrued = new uint256[](16); 
        uint256[] memory accruedOptimized = new uint256[](16); 

        // Create sample StakeData with different values
        for (uint i = 1; i <= 15; i++) {
            StakeData memory stake = StakeData(
                1000,
                uint32(block.timestamp - (30 days * i)),
                false,
                StakingDuration.OneYear
            );
            accrued[i] = calculateAccruedVvvAmount(stake);
            accruedOptimized[i] = calculateAccruedVvvAmount_Optimized(stake);
        }

        return (accrued, accruedOptimized);
    }
}
```

## Code Snippet
issue code is 
```solidity
        nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;
        
        accruedVvv =
            (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
            DENOMINATOR;
```

## Tool used
Manual Aided Review

## Recommendation
compute `accruedVvv` as follows :-
```solidity

        nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount);

        accruedVvv =
            (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) /
            stakeDuration /       //re-arranged division
            DENOMINATOR;
    }
```