Immense Purple Bat

medium

# Loss of precision in the vested amount calculation

## Summary

The contest README states:
```md
We accept a loss of precision of 0.000000000001% on the vested amount calculation.
```
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/README.md?plain=1#L68

but values much larger than this may be lost


## Vulnerability Detail

The `calculateVestedAmountAtInterval()` function does a multiplication on the result of a division, which causes a loss of precision. `x.mulWadDown(y).divWadDown(z)` is equivalent to `(uint256(x * y / WAD) * WAD) / z`, so the part inside the `uint256` ends up losing precision.


## Impact

Precision loss in excess of the `0.000000000001%` README percentage (see the PoC section below)


## Code Snippet

```solidity
// File: contracts/vesting/VVVVesting.sol : VVVVesting.calculateVestedAmountAtInterval()   #1

294                    // Calculate the sum of the geometric series
295 @>                 Sn = _firstIntervalAccrual.mulWadDown((rToN - FixedPointMathLib.WAD)).divWadDown(
296                        r - FixedPointMathLib.WAD
297:                   );
```
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/vesting/VVVVesting.sol#L294-L297



## Tool used

Manual Review


## Recommendation

Use solady's [`fullMulDiv()`](https://github.com/Vectorized/solady/blob/e4a14a5b365b353352f7c38e699a2bc9363d6576/src/utils/FixedPointMathLib.sol#L436) instead of `mulWadDown()` followed by `divWadDown()`


## PoC

The foundry fuzzing test below compares the values returned by the existing version of the function, and a modified version using `fullMulDiv()`. It uses bounds checks on the input variable that come from other tests within the project, to ensure reasonable values. It's currently set to a specific set of inputs for ease of running, but can be modified to find values within any range.

The current set of inputs is `calculateVestedAmountAtInterval(1000043509869935391, 1, 1000000)`, which means an initial value of ~1ETH, value at the first interval, with a growth rate of 0.0000000001%

Using `chisel`, we see this results in a value of `1000043000000000000`, but the actual value should be `1000043509869935391`, which is a `0.000051%` difference, well past the sponsor's threshold of `0.000000000001%`:
```text
➜ import { VVVVesting } from "contracts/vesting/VVVVesting.sol"
➜ import { FixedPointMathLib } from "@solmate/src/utils/FixedPointMathLib.sol"
➜ VVVVesting vi = new VVVVesting(address(0x1), address(0x1))
➜ vi.calculateVestedAmountAtInterval(1000043509869935391, 1, 1000000)
Type: uint256
├ Hex: 0x0000000000000000000000000000000000000000000000000de0ddcf5f1db000
├ Hex (full word): 0x0000000000000000000000000000000000000000000000000de0ddcf5f1db000
└ Decimal: 1000043000000000000
➜ uint256(uint256(1000043509869935391) * (uint256(1e18+1000000)**1 - 1e18)) / ((1e18+1000000) - 1e18)
Type: uint256
├ Hex: 0x0000000000000000000000000000000000000000000000000de0de4615bb7b1f
├ Hex (full word): 0x0000000000000000000000000000000000000000000000000de0de4615bb7b1f
└ Decimal: 1000043509869935391
```

As can be seen in online partial-geometric-sum calculators, `1000043509869935391` is the [correct](https://planetcalc.com/8599/?first=10000031000043509869935391&d=1000000000001000000&n=1) value (also, logically, it's the first interval, so it should return the input argument directly without modification). As the input amount gets smaller, the percentage loss becomes larger (e.g. `vi.calculateVestedAmountAtInterval(100000000000, 1, 1000000)` returns `0`, which is a loss of `100%`

<details><summary>Full fuzzing test</summary>

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.23;

import { Test } from "lib/forge-std/src/Test.sol";

contract CalcTest is Test {

    // forge test --match-test testIt -vvvvvv --fuzz-runs 10240
    function testIt(uint256 f, uint256 e, uint256 g) public {
         f = bound(f, 1 ether, 1_000_000_000 ether);
         e = bound(e, 1, 250);
         g = bound(g, 1000000, WAD * 34 / 100);

         // hard-code values
         // https://planetcalc.com/8599/?first=10000031000043509869935391&d=1000000000001000000&n=1
         // uint256(uint256(1000043509869935391) * (uint256(1e18+1000003)**1 - 1e18)) / ((1e18+1000003) - 1e18)
         // vi.calculateVestedAmountAtInterval(1000043509869935391, 1, 1000000)
         f = 1000043509869935391; e = 1; g = 1000000;
         
         uint256 oldc = calculateVestedAmountAtIntervalOrig(f, e, g);
         uint256 newc = calculateVestedAmountAtIntervalNew(f, e, g);
         uint256 thresh = oldc * 1 / 100000000;
         emit Results(oldc, newc, fullMulDiv(WAD, (oldc > newc ? oldc - newc : newc - oldc), oldc), thresh);
         assertApproxEqAbs(oldc, newc, thresh);
    }
    
    event Results(uint256 o, uint256 n, uint256 pct, uint256 thresh);

    uint256 internal constant MAX_UINT256 = 2**256 - 1;
    
    uint256 internal constant WAD = 1e18; // The scalar of ETH and most ERC20s.
    
    function mulWadDown(uint256 x, uint256 y) private pure returns (uint256) {
        return mulDivDown(x, y, 1e18); // Equivalent to (x * y) / 1e18 rounded down.
    }
    
    function divWadDown(uint256 x, uint256 y) private pure returns (uint256) {
        return mulDivDown(x, 1e18, y); // Equivalent to (x * 1e18) / y rounded down.
    }
    
    function mulDivDown(
        uint256 x,
        uint256 y,
        uint256 denominator
    ) private pure returns (uint256 z) {
        /// @solidity memory-safe-assembly
        assembly {
            // Equivalent to require(denominator != 0 && (y == 0 || x <= type(uint256).max / y))
            if iszero(mul(denominator, iszero(mul(y, gt(x, div(MAX_UINT256, y)))))) {
                revert(0, 0)
            }

            // Divide x * y by the denominator.
            z := div(mul(x, y), denominator)
        }
    }
    
    function rpow(
        uint256 x,
        uint256 n,
        uint256 scalar
    ) private pure returns (uint256 z) {
        /// @solidity memory-safe-assembly
        assembly {
            switch x
            case 0 {
                switch n
                case 0 {
                    // 0 ** 0 = 1
                    z := scalar
                }
                default {
                    // 0 ** n = 0
                    z := 0
                }
            }
            default {
                switch mod(n, 2)
                case 0 {
                    // If n is even, store scalar in z for now.
                    z := scalar
                }
                default {
                    // If n is odd, store x in z for now.
                    z := x
                }

                // Shifting right by 1 is like dividing by 2.
                let half := shr(1, scalar)

                for {
                    // Shift n right by 1 before looping to halve it.
                    n := shr(1, n)
                } n {
                    // Shift n right by 1 each iteration to halve it.
                    n := shr(1, n)
                } {
                    // Revert immediately if x ** 2 would overflow.
                    // Equivalent to iszero(eq(div(xx, x), x)) here.
                    if shr(128, x) {
                        revert(0, 0)
                    }

                    // Store x squared.
                    let xx := mul(x, x)

                    // Round to the nearest number.
                    let xxRound := add(xx, half)

                    // Revert if xx + half overflowed.
                    if lt(xxRound, xx) {
                        revert(0, 0)
                    }

                    // Set x to scaled xxRound.
                    x := div(xxRound, scalar)

                    // If n is even:
                    if mod(n, 2) {
                        // Compute z * x.
                        let zx := mul(z, x)

                        // If z * x overflowed:
                        if iszero(eq(div(zx, x), z)) {
                            // Revert if x is non-zero.
                            if iszero(iszero(x)) {
                                revert(0, 0)
                            }
                        }

                        // Round to the nearest number.
                        let zxRound := add(zx, half)

                        // Revert if zx + half overflowed.
                        if lt(zxRound, zx) {
                            revert(0, 0)
                        }

                        // Return properly scaled zxRound.
                        z := div(zxRound, scalar)
                    }
                }
            }
        }
    }
    
    function calculateVestedAmountAtIntervalOrig(
        uint256 _firstIntervalAccrual,
        uint256 _elapsedIntervals,
        uint256 _growthRateProportion
    ) private pure returns (uint256) {
        if (_growthRateProportion == 0 || _elapsedIntervals == 0) {
            return _firstIntervalAccrual * _elapsedIntervals;
        } else {
            uint256 r;
            uint256 rToN;
            uint256 Sn;
            unchecked {
                // Convert growth rate proportion to a fixed-point number with 1e18 scale
                r = divWadDown(
                    _growthRateProportion + WAD,
                    WAD
                );

                // Calculate r^n
                rToN = rpow(r, _elapsedIntervals, WAD);

                // Calculate the sum of the geometric series
                Sn = divWadDown(mulWadDown(_firstIntervalAccrual, (rToN - WAD)),
                    r - WAD
                );
            }
            return Sn;
        }
    }

    function calculateVestedAmountAtIntervalNew(
        uint256 _firstIntervalAccrual,
        uint256 _elapsedIntervals,
        uint256 _growthRateProportion
    ) private pure returns (uint256) {
        if (_growthRateProportion == 0 || _elapsedIntervals == 0) {
            return _firstIntervalAccrual * _elapsedIntervals;
        } else {
            uint256 r;
            uint256 rToN;
            uint256 Sn;
            unchecked {
                // Convert growth rate proportion to a fixed-point number with 1e18 scale
                r = divWadDown(
                    _growthRateProportion + WAD,
                    WAD
                );

                // Calculate r^n
                rToN = rpow(r, _elapsedIntervals, WAD);

                // Calculate the sum of the geometric series
                Sn = fullMulDiv(_firstIntervalAccrual, (rToN - WAD),
                    r - WAD
                );
            }
            return Sn;
        }
    }

    function fullMulDiv(uint256 x, uint256 y, uint256 d) internal pure returns (uint256 result) {
        /// @solidity memory-safe-assembly
        assembly {
            for {} 1 {} {
                // 512-bit multiply `[p1 p0] = x * y`.
                // Compute the product mod `2**256` and mod `2**256 - 1`
                // then use the Chinese Remainder Theorem to reconstruct
                // the 512 bit result. The result is stored in two 256
                // variables such that `product = p1 * 2**256 + p0`.

                // Least significant 256 bits of the product.
                result := mul(x, y) // Temporarily use `result` as `p0` to save gas.
                let mm := mulmod(x, y, not(0))
                // Most significant 256 bits of the product.
                let p1 := sub(mm, add(result, lt(mm, result)))

                // Handle non-overflow cases, 256 by 256 division.
                if iszero(p1) {
                    if iszero(d) {
                        mstore(0x00, 0xae47f702) // `FullMulDivFailed()`.
                        revert(0x1c, 0x04)
                    }
                    result := div(result, d)
                    break
                }

                // Make sure the result is less than `2**256`. Also prevents `d == 0`.
                if iszero(gt(d, p1)) {
                    mstore(0x00, 0xae47f702) // `FullMulDivFailed()`.
                    revert(0x1c, 0x04)
                }

                /*------------------- 512 by 256 division --------------------*/

                // Make division exact by subtracting the remainder from `[p1 p0]`.
                // Compute remainder using mulmod.
                let r := mulmod(x, y, d)
                // `t` is the least significant bit of `d`.
                // Always greater or equal to 1.
                let t := and(d, sub(0, d))
                // Divide `d` by `t`, which is a power of two.
                d := div(d, t)
                // Invert `d mod 2**256`
                // Now that `d` is an odd number, it has an inverse
                // modulo `2**256` such that `d * inv = 1 mod 2**256`.
                // Compute the inverse by starting with a seed that is correct
                // correct for four bits. That is, `d * inv = 1 mod 2**4`.
                let inv := xor(2, mul(3, d))
                // Now use Newton-Raphson iteration to improve the precision.
                // Thanks to Hensel's lifting lemma, this also works in modular
                // arithmetic, doubling the correct bits in each step.
                inv := mul(inv, sub(2, mul(d, inv))) // inverse mod 2**8
                inv := mul(inv, sub(2, mul(d, inv))) // inverse mod 2**16
                inv := mul(inv, sub(2, mul(d, inv))) // inverse mod 2**32
                inv := mul(inv, sub(2, mul(d, inv))) // inverse mod 2**64
                inv := mul(inv, sub(2, mul(d, inv))) // inverse mod 2**128
                result :=
                    mul(
                        // Divide [p1 p0] by the factors of two.
                        // Shift in bits from `p1` into `p0`. For this we need
                        // to flip `t` such that it is `2**256 / t`.
                        or(
                            mul(sub(p1, gt(r, result)), add(div(sub(0, t), t), 1)),
                            div(sub(result, r), t)
                        ),
                        // inverse mod 2**256
                        mul(inv, sub(2, mul(d, inv)))
                    )
                break
            }
        }
    }
}
```
</details>
