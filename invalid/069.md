Plain Macaroon Tuna

medium

# Inconsistent behavior when claiming vVvToken after `ethToVvvExchangeRate` is changed, resulting in user got more/less vVvToken after months of staking

## Summary

The value of `ethToVvvExchangeRate` function used in `VVVETHStaking::calculateAccruedVvvAmount` can be changed in the future, making the `claimVvv` function behave inconsistently in regard of exchange rate at the time of user call `stakeEth` or `restakeEth`.

## Vulnerability Detail

as per documentation [provided](https://hackmd.io/@vvv-knowledge/Syme5HlRT) `ethToVvvExchangeRate` is set to 1 but confirmed to be changed in future using function which determines the exchange rate.
if this not handled correctly, it can cause user to get more or less intended amount of `vVvToken` because `ethToVvvExchangeRate` is directly called whenever user want to claim `vVvToken`

Proof of Concept:

1. user stake `1 weth` for 3 months, current `ethToVvvExchangeRate` is `1` and user expected to get `1 vVvToken`.
2. after 2 months, Protocol decided to change `ethToVvvExchangeRate` to `2`.
3. after the staking period is done, user decided to claim vVvToken and they got `2 vVvToken`.
4. user call `restakeEth` with the current `_stakeId`, hoping to get `2 vVvToken` in 3 months.
5. just after step 4, protocol decided to change the `ethToVvvExchangeRate` to `0.5`.
6. after the staking period is done, user claim vVvToken and they got `0.5 vVvToken`.
7. user expect to get `3 vVvToken` in 6 month of staking `1 weth` but got `2.5 vVvToken` instead.

another scenario can happen too when user got more `vVvToken` than intended.

## Impact

inconsistent behavior when claiming vVvToken after `ethToVvvExchangeRate` is changed

## Code Snippet

[permalink to code](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L237C38-L237C58)

`VVVETHStaking.sol`:

```javascript
    function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration ? stakeDuration : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

            accruedVvv =
@>            (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) / DENOMINATOR;
        }

        return accruedVvv;
    }
```

## Tool used

Manual Review

## Recommendation

add one more element in StakeData struct and assign it the current `ethToVvvExchangeRate` value in `VVVETHStaking.sol` contract

<details><summary>code</summary>

```diff
    struct StakeData {
        uint224 stakedEthAmount;
        uint32 stakeStartTimestamp;
        bool stakeIsWithdrawn;
        StakingDuration stakeDuration;
+       uint256 exchangeRate;
    }
.
.
.
    function calculateAccruedVvvAmount(StakeData memory _stake) public view returns (uint256) {
        uint256 stakeDuration = durationToSeconds[_stake.stakeDuration];
        uint256 secondsSinceStakingStarted = block.timestamp - _stake.stakeStartTimestamp;
        uint256 secondsStaked;
        uint256 nominalAccruedEth;
        uint256 accruedVvv;

        unchecked {
            secondsStaked = secondsSinceStakingStarted >= stakeDuration ? stakeDuration : secondsSinceStakingStarted;

            nominalAccruedEth = (secondsStaked * _stake.stakedEthAmount) / stakeDuration;

            accruedVvv =
-            (nominalAccruedEth * ethToVvvExchangeRate() * durationToMultiplier[_stake.stakeDuration]) / DENOMINATOR;
+            (nominalAccruedEth * _stake.exchangeRate * durationToMultiplier[_stake.stakeDuration]) / DENOMINATOR;
        }

        return accruedVvv;
    }
.
.
.
    function _stakeEth(StakingDuration _stakeDuration, uint256 _stakedEthAmount) private {
        if (_stakedEthAmount == 0) revert CantStakeZeroEth();
        ++stakeId;

        userStakes[msg.sender][stakeId] = StakeData({
            stakedEthAmount: uint224(_stakedEthAmount),
            stakeStartTimestamp: uint32(block.timestamp),
            stakeIsWithdrawn: false,
            stakeDuration: _stakeDuration,
+           exchangeRate: ethToVvvExchangeRate()
        });

        _userStakeIds[msg.sender].push(stakeId);

        emit Stake(msg.sender, stakeId, uint224(_stakedEthAmount), uint32(block.timestamp), _stakeDuration);
    }

```

</details>
