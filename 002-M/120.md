Immense Purple Bat

medium

# Users may DoS themselves with too many stakes

## Summary

Users may DoS themselves with too many stakes


## Vulnerability Detail

Calculating the claimable amount involves iterating over every prior stake, and summing them. If a user has a lot of stakes over time (e.g. a daily stake over many years - think a user staking every output of a Sablier stream), they'll be able to individually perform each stake, but when it's time to claim, they'll be unable to, because the transaction will be larger than the block gas limit.


## Impact

Accidental self DoS, requiring an admin intervention to rescue the funds


## Code Snippet

Calculating rewards involves reading the full storage array of the user's stakes into memory (one SLOAD each), and then reading each `StakeData` struct, which adds another two SLOADs each:
```solidity
// File: contracts/staking/VVVETHStaking.sol : VVVETHStaking.calculateAccruedVvvAmount()   #1

195    
196        /**
197            @notice Returns accrued $VVV for a user based on their staking activity
198            @dev Does not account for any claimed tokens
199            @return $VVV accrued
200         */
201        function calculateAccruedVvvAmount() public view returns (uint256) {
202 @>         uint256[] memory stakeIds = _userStakeIds[msg.sender];
203            if (stakeIds.length == 0) return 0;
204    
205            uint256 totalVvvAccrued;
206            for (uint256 i = 0; i < stakeIds.length; ++i) {
207 @>             StakeData memory stake = userStakes[msg.sender][stakeIds[i]];
208                unchecked {
209                    totalVvvAccrued += calculateAccruedVvvAmount(stake);
210                }
211            }
212    
213            return totalVvvAccrued;
214        }
215:   
```
https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/staking/VVVETHStaking.sol#L195-L215


## Tool used

Manual Review


## Recommendation

Store and manage the total separately as stakes get added/removed, and use that storage value instead of iterating


## PoC

The block gas limit is 30 million, and adding ~550 stakes bumps one over this limit:
```diff
diff --git a/vvv-platform-smart-contracts/test/staking/VVVETHStaking.unit.t.sol b/vvv-platform-smart-contracts/test/staking/VVVETHStaking.unit.t.sol
index e92c1cc..92cf73d 100644
--- a/vvv-platform-smart-contracts/test/staking/VVVETHStaking.unit.t.sol
+++ b/vvv-platform-smart-contracts/test/staking/VVVETHStaking.unit.t.sol
@@ -153,6 +153,27 @@ contract VVVETHStakingUnitTests is VVVETHStakingTestBase {
         vm.stopPrank();
     }
 
+    // Tests that a user can't DOS themselves with too many stakes
+    function testStakeEthMultipleDos() public {
+        vm.startPrank(sampleUser, sampleUser);
+
+        uint256 stakeEthAmount = 100;
+        //for (uint256 i; i < 0; ++i) { // 5697 gas
+        for (uint256 i; i < 550; ++i) { // 32219519 gas
+            EthStakingInstance.stakeEth{ value: stakeEthAmount }(
+                VVVETHStaking.StakingDuration.ThreeMonths
+            );
+        }
+        /*
+        advanceBlockNumberAndTimestampInSeconds(
+            EthStakingInstance.durationToSeconds(VVVETHStaking.StakingDuration.ThreeMonths) + 1
+        );
+        uint256 claimableVvv = EthStakingInstance.calculateClaimableVvvAmount();
+        EthStakingInstance.claimVvv(claimableVvv);*/
+
+        vm.stopPrank();
+    }
+
     // Tests that a user cannot stake 0 ETH
     function testStakeZeroEth() public {
         vm.startPrank(sampleUser, sampleUser);
```

The above shows that adding that many will result in the writes taking that much gas, but claiming should result in approximately the same amount used. I tried the following test on remix to prove it, but the execution kept hanging:

<details><summary>Full remix test</summary>

```solidity
// SPDX-License-Identifier: MIT

pragma solidity 0.8.19;

contract VVVETHStaking {
    mapping(address => mapping(uint256 => StakeData)) internal userStakes;
    mapping(address => uint256[]) private _userStakeIds;
    mapping(address => uint256) internal userVvvClaimed;

    uint256 internal stakeId;

    enum StakingDuration {
        ThreeMonths,
        SixMonths,
        OneYear
    }
    
    struct StakeData {
        uint224 stakedEthAmount;
        uint32 stakeStartTimestamp;
        bool stakeIsWithdrawn;
        StakingDuration stakeDuration;
    }

    function stakeEth(
        StakingDuration _stakeDuration
    ) public payable returns (uint256) {
        _stakeEth(_stakeDuration, 1);
        return stakeId;
   }
   
   function _stakeEth(StakingDuration _stakeDuration, uint256 _stakedEthAmount) private {
        ++stakeId;

        userStakes[msg.sender][stakeId] = StakeData({
            stakedEthAmount: uint224(_stakedEthAmount),
            stakeStartTimestamp: uint32(block.timestamp),
            stakeIsWithdrawn: false,
            stakeDuration: _stakeDuration
        });

        _userStakeIds[msg.sender].push(stakeId);
    }
    
    function stakeMany(uint256 amount) public {
        for (uint256 i; i < amount; ++i) {
            stakeEth(StakingDuration.ThreeMonths);
        }
    }
    
    function calculateAccruedVvvAmount() public view returns (uint256) {
        uint256[] memory stakeIds = _userStakeIds[msg.sender];

        uint256 totalVvvAccrued;
        for (uint256 i = 0; i < stakeIds.length; ++i) {
            StakeData memory stake = userStakes[msg.sender][stakeIds[i]];
            unchecked {
                totalVvvAccrued += stake.stakedEthAmount;
            }
        }

        return totalVvvAccrued;
    }
}
```