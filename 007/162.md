Hollow Punch Chinchilla

high

# Updating vesting schedule can cause users to receive more amounts of tokens if the current vesting schedule is updated after the `_vestingScheduleStartTime` has passed

## Summary

The `VVVVesting.sol::withdrawVestedTokens()` allows users to claim part of their vesting depending on the time passed. The `VestingSchedule` struct contains variables which are used in defining amounts to be claimed by users depending on the interval/time.

The two variables which can cause issue when updating vestings are `tokensToVestAtStart` and `scheduleStartTime`.

Users can claim allocated amount of tokens at start i.e `_tokensToVestAtStart` more than once depending on how many times the vesting schedule has been updated.

## Vulnerability Detail

When updating the Vesting Schedule of a user once the current `block.timestamp` has reached the `scheduleStartTime` can allow the user to claim `_tokenToVestAtStart` more than once. The function `VVVVesting.sol::withdrawVestedTokens` uses `_tokenAmountToWithdraw` and `_vestingScheduleIndex` as parameters.


```solidity
function withdrawVestedTokens(
        uint128 _tokenAmountToWithdraw,
        address _tokenDestination,
        uint256 _vestingScheduleIndex
    ) external {
        VestingSchedule[] storage vestingSchedules = userVestingSchedules[msg.sender];

        if (_vestingScheduleIndex >= vestingSchedules.length) {
            revert InvalidScheduleIndex();
        }

        VestingSchedule storage vestingSchedule = vestingSchedules[_vestingScheduleIndex];

        if (
            _tokenAmountToWithdraw >
            getVestedAmount(msg.sender, _vestingScheduleIndex) - vestingSchedule.tokenAmountWithdrawn
        ) {
            revert AmountIsGreaterThanWithdrawable();
        }

        vestingSchedule.tokenAmountWithdrawn += _tokenAmountToWithdraw;

        VVVToken.safeTransfer(_tokenDestination, _tokenAmountToWithdraw);
        
        // Events below
```

In the second If condition, we move to `VVVVesting.sol::getVestedAmount` where we check

```solidity
        function getVestedAmount(
        address _vestedUser,
        uint256 _vestingScheduleIndex
    ) public view returns (uint256) {
        VestingSchedule storage vestingSchedule = userVestingSchedules[_vestedUser][_vestingScheduleIndex];

        if (
            block.timestamp < vestingSchedule.scheduleStartTime ||
            vestingSchedule.scheduleStartTime == 0 ||
            userVestingSchedules[_vestedUser].length == 0
        ) {
            return 0;
        } else if (block.timestamp < vestingSchedule.cliffEndTime) {
            return vestingSchedule.tokensToVestAtStart;
        } else {
            uint256 elapsedIntervals;
            uint256 vestedAmount;

            unchecked {
                elapsedIntervals =
                    (block.timestamp - vestingSchedule.cliffEndTime) /
                    vestingSchedule.intervalLength;
                elapsedIntervals = elapsedIntervals > vestingSchedule.maxIntervals
                    ? vestingSchedule.maxIntervals
                    : elapsedIntervals;
                vestedAmount =
                    vestingSchedule.tokensToVestAtStart +
                    calculateVestedAmountAtInterval(
                        vestingSchedule.tokensToVestAfterFirstInterval,
                        elapsedIntervals,
                        vestingSchedule.growthRateProportion
                    );
            }

            return vestedAmount;
        }
    }
```

If the `block.timestamp` has passed over `vestingSchedule.scheduleStartTime` and has not passed over the `cliffEndTime` users can claim their `_tokenToVestAtStart`, if the `block.timestamp` has passed over `cliffEndTime`, users will be transferred `tokensToVestAtStart` + the amount they have allocated.

The vulnerability exists when the user has already claimed `tokensToVestAtStart` and then the vesting gets updated by admin, this allows User to claim the new `tokensToVestAtStart` again once the `scheduleStartTime` is reached 

### Proof of Concept

The scenerio is : 

1. User is created a vesting schedule which allows them to claim 1_000 * e18 (`tokensToVestAtStart`) tokens at `scheduleStartTime`
2. The `block.timestamp` reaches `scheduleStartTime` and user claims `tokensToVestAtStart`
3. The admin updates the schedule for the user with different `tokensToVestAtStart` and `scheduleStartTime`
4. The user can now claim again once the `block.timestamp` reaches the new `scheduleStartTime`

The proof of concept uses the same function used by devs to test updating the vesting schedule for a user with added lines of code for withdrawals by user

```solidity
function testDoubleClaimingAfterUpdating() public {
        {
            VestingParams memory params = VestingParams({
                vestingScheduleIndex: 0,
                tokensToVestAtStart: 1_000 * 1e18, //1k tokens
                tokensToVestAfterFirstInterval: 100 * 1e18, //100 tokens
                amountWithdrawn: 0,
                scheduleStartTime: uint32(block.timestamp + 60 * 60 * 24 * 2), // 2 days
                cliffEndTime: uint32(block.timestamp + 60 * 60 * 24 * 365),
                intervalLength: 12,
                maxIntervals: 100,
                growthRateProportion: 0
            });

            setVestingScheduleFromManager(
                sampleUser,
                params.vestingScheduleIndex,
                params.tokensToVestAtStart,
                params.tokensToVestAfterFirstInterval,
                params.amountWithdrawn,
                params.scheduleStartTime,
                params.cliffEndTime,
                params.intervalLength,
                params.maxIntervals,
                params.growthRateProportion
            );

            (
                uint256 _tokensToVestAtStart,
                uint256 _tokensToVestAfterFirstInterval,
                uint256 _intervalLength,
                uint256 _maxIntervals,
                uint256 _tokenAmountWithdrawn,
                uint256 _scheduleStartTime,
                uint256 _cliffEndTime,
                uint256 _growthRateProportion
            ) = VVVVestingInstance.userVestingSchedules(sampleUser, 0);

            assertTrue(_tokensToVestAtStart == params.tokensToVestAtStart);
            assertTrue(_tokensToVestAfterFirstInterval == params.tokensToVestAfterFirstInterval);
            assertTrue(_tokenAmountWithdrawn == 0);
            assertTrue(_scheduleStartTime == params.scheduleStartTime);
            assertTrue(_cliffEndTime == params.cliffEndTime);
            assertTrue(_intervalLength == params.intervalLength);
            assertTrue(_maxIntervals == params.maxIntervals);
            assertTrue(_growthRateProportion == params.growthRateProportion);
        }

        vm.warp(block.timestamp + 2 days);

        vm.prank(sampleUser);
        VVVVestingInstance.withdrawVestedTokens(1_000 * 1e18, address(sampleUser), 0);
        vm.stopPrank();

        {
            //update part of schedule (tokensToVestAfterStart is now 2k, postCliffDuration is now 3 years)
            VestingParams memory params = VestingParams({
                vestingScheduleIndex: 0,
                tokensToVestAtStart: 2_000 * 1e18, //2k tokens
                tokensToVestAfterFirstInterval: 200 * 1e18, //200 tokens
                amountWithdrawn: 0,
                scheduleStartTime: uint32(block.timestamp + 60 * 60 * 24 * 3), //3 days from now
                cliffEndTime: uint32(block.timestamp + 60 * 60 * 24 * 180), //180 days from scheduleStartTime
                intervalLength: 12,
                maxIntervals: 200,
                growthRateProportion: 0
            });

            setVestingScheduleFromManager(
                sampleUser,
                params.vestingScheduleIndex,
                params.tokensToVestAtStart,
                params.tokensToVestAfterFirstInterval,
                params.amountWithdrawn,
                params.scheduleStartTime,
                params.cliffEndTime,
                params.intervalLength,
                params.maxIntervals,
                params.growthRateProportion
            );

            (
                uint256 _tokensToVestAtStart,
                uint256 _tokensToVestAfterFirstInterval,
                uint256 _intervalLength,
                uint256 _maxIntervals,
                uint256 _tokenAmountWithdrawn,
                uint256 _scheduleStartTime,
                uint256 _cliffEndTime,
                uint256 _growthRateProportion
            ) = VVVVestingInstance.userVestingSchedules(sampleUser, 0);

            assertTrue(_tokensToVestAtStart == params.tokensToVestAtStart);
            assertTrue(_tokensToVestAfterFirstInterval == params.tokensToVestAfterFirstInterval);
            assertTrue(_tokenAmountWithdrawn == 0);
            assertTrue(_scheduleStartTime == params.scheduleStartTime);
            assertTrue(_cliffEndTime == params.cliffEndTime);
            assertTrue(_intervalLength == params.intervalLength);
            assertTrue(_maxIntervals == params.maxIntervals);
            assertTrue(_growthRateProportion == params.growthRateProportion);
        }

        vm.warp(block.timestamp + 3 days);

        vm.prank(sampleUser);
        VVVVestingInstance.withdrawVestedTokens(2_000 * 1e18, address(sampleUser), 0);
        vm.stopPrank();
    }
```

As shown in the PoC, users can claim their starting amount again if the vesting schedule is updated after the current start time has passed. The PoC only mentions claiming of `tokensToVestAtStart` but if the vesting schedule is updated even later such as after the first interval then this will also result in the user being able to claim more than they are supposed to.

## Impact

Users can claim more than what they are supposed to due to no check on updating vesting schedule to execute only if the current `scheduleStartTime` has not passed.

## Code Snippet

[`setVestingSchedule`](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/vesting/VVVVesting.sol#L317-L346)

[`batchSetVestingSchedule`](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/vesting/VVVVesting.sol#L353-L357)

[`_setVestingSchedule`](https://github.com/sherlock-audit/2024-03-vvv-vesting-staking/blob/main/vvv-platform-smart-contracts/contracts/vesting/VVVVesting.sol#L191-L214)

## Tool used

Manual Review, Foundry

## Recommendation

Introduce a check in `_setVestingSchedule` to compare the current `scheduleStartTime` to the current `block.timestamp`, if the current has passed then revert the call to update.

